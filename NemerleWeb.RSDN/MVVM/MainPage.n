using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Extensions;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.Rsdn
{  
  [Unit]
  public class MainPage
  {
    mutable _searchTerm : string;
    
    Root : TreeNode { get; set; }
    SearchResults : IEnumerable[(TreeNode * list[string])] { get; set; }
    
    SearchTerm : string { 
      get { _searchTerm; }
      set {
          _searchTerm = value;
          Search(value);
      }; 
    }
    
    public this() {
      LoadTopNodes();
    }
    
    public LoadTopNodes() : void
    {
      server.GetTopNodes(nodes => {
        Root = TreeNode() <- (
          Children = nodes
        );
      });
    }
    
    public Search(term : string) : void
    {
      if(string.IsNullOrWhiteSpace(term))
        LoadTopNodes();
      else {        
        server.Search(term, nodes => {
          SearchResults = nodes
        });
      }
    }
    
    [Html]
    public View() : string
    {
      <#
        <div $when(Root != null) class="root">
          <div>
            <input value="$SearchTerm" />
          </div>
          <div $foreach((node, path) in SearchResults)>
            <span>$(node.Caption)</span>
          </div>
          <div $when(string.IsNullOrEmpty(SearchTerm))>
            <div template="$(template(Root))" />
          </div>
        </div>
      #>
    }
    
    public class Server
    {
      public GetTopNodes() : List[TreeNode]
      {
        TreeLoader.GetTopNodes()
      }
      
      public Search(term : string) : IEnumerable[(TreeNode * list[string])]
      {
        TreeLoader.Search(term)
      }
    }
  }
}
