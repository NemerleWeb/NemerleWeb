using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Extensions;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.Rsdn
{  
  [Unit]
  public class MainPage
  {
    Root : TreeNode { get; set; }
    SearchTerm : string { get; set; }
    
    public this() {
      LoadTopNodes();
    }
    
    public LoadTopNodes() : void
    {
      server.GetTopNodes(nodes => {
        Root = TreeNode() <- (
          Children = nodes
        );
      });
    }
    
    public Search() : void
    {
      if(string.IsNullOrWhiteSpace(SearchTerm))
        LoadTopNodes();
      else 
        server.Search(SearchTerm, nodes => {
          Root = TreeNode() <- (
            Children = nodes
          );
        });
    }
    
    [Html]
    public View() : string
    {
      <#
        <div $when(Root != null) class="root">
          <div>
            <input value="$SearchTerm" event-keyup="$Search" />
          </div>
          <div template="$(template(Root))" />
        </div>
      #>
    }
    
    public class Server
    {
      public GetTopNodes() : List[TreeNode]
      {
        TreeLoader.GetTopNodes()
      }
      
      public Search(term : string) : List[TreeNode]
      {
        TreeLoader.Search(term)
      }
    }
  }
}
