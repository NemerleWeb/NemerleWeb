using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using NemerleWeb;

namespace NemerleWeb.Rsdn
{  
  [Unit]
  public class TreeNode
  {
    public Id : string { get; set; }
    public Caption : string { get; set; }
    public Href : string { get; set; }
    public Icon : string { get; set; }
    public Hide : bool { get; set; }
    public Depth : int { get; set; }
    public IsOpened : bool { get; set; }
    public Children : List[TreeNode] { get; set; }    
    public HasChildren : bool { get; set; }
    
    public IconUrl : string 
    {      
      get {        
        def suffix = match(IsOpened, HasChildren) {
                      | (true, _) => "o.gif";
                      | (false, true) => "c.gif" 
                      | _ => "s.gif";
                     };
          "http://rsdn.ru/images/tree/" + Icon + suffix;
      }
    }
    
    public Toggle() : void
    {
      if(Children == null) {        
        server.LoadNodeChildren(Id, children => {
          Children = children;
          IsOpened = !IsOpened;
        });
      } else {
        IsOpened = !IsOpened;
      }
    }
    
    public SelectNode() : void
    {
      MainPage.SelectedUrl = Href;
    }
    
    //<span $when(c.IsOpened)>-</span>
    //<span $when(!c.IsOpened)>+</span>              
    [Html]
    public View() : string
    {
      <#
        <div $foreach(c in Children)>   
          <div style-margin-left="$(c.Depth * 10)">
            <img attr-src="$(c.IconUrl)" />
            <a $when(c.HasChildren) click="$(c.Toggle())" class="node with-children">
              
              <span>$(c.Caption)</span>
            </a>
            <a $when(!c.HasChildren) click="$(c.SelectNode())" class="node">
              <span>$(c.Caption)</span>
            </a>            
            <div $when(c.IsOpened && Children != null)>
              <div template="$(template(c))" />
            </div>          
          </div>
        </div>
      #>
    }
    
    public class Server
    {
      public LoadNodeChildren(id : string) : List[TreeNode]
      {
        TreeLoader.LoadNodeChildren(id)
      }
    }
  }
}
