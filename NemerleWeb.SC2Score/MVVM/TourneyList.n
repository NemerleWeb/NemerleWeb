using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.SC2Score
{ 
  [Unit]
  public class TourneyList
  {
    public PinnedTourneys : List[Tourney] { get; set; }
    public NormalTourneys : List[Tourney] { get; set; }
    public Selected : Tourney { get; set; }
    
    public this()
    {
      server.LoadFrontPageTourneys(tourneys => {         
        PinnedTourneys = tourneys.Take(3).ToList();
        NormalTourneys = tourneys.Skip(3).ToList();
      });
    }
    
    public SelectTourney(tourney : Tourney) : void
    {
      Selected = tourney;
    }
        
    public class Server
    {
      public LoadFrontPageTourneys() : List[Tourney]
      {
        [Tourney("DreamHack Open - Bucharest"), Tourney("WCS Asia Finals"), Tourney("NASL Season 4"), Tourney("IPTL Season 1"),
         Tourney("GSTL Season 3"), Tourney("MLG vs Proleague Invit."), Tourney("OSL Season 1"), Tourney("GSL Season 4")].ToList();
      }
    }
    
    [Html]
    public View() : string
    {
      <#
        <div class="tourney-list-container">
          <h2>Pinned</h2>
          <ul class="tourney-list">
            <li $foreach(t in PinnedTourneys) 
                click="$SelectTourney"
                class="btn btn-info">$(t.Name)</li>
          </ul>
          <h2>Rest</h2>
          <ul class="tourney-list">
            <li $foreach(t in NormalTourneys) 
                click="$SelectTourney"
                class="btn btn-info">$(t.Name)</li>
          </ul>
        </div>
      #>
    }
  }
}
