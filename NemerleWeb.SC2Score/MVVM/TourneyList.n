using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.SC2Score
{ 
  [Unit]
  public class TourneyList
  {
    public PinnedTourneys : List[TourneyShortInfo] { get; set; }
    public NormalTourneys : List[TourneyShortInfo] { get; set; }
    public Selected : Tourney { get; set; }
    
    public this()
    {
      server.Recent50(tourneys => {         
        PinnedTourneys = tourneys.Take(3).ToList();
        NormalTourneys = tourneys.Skip(3).ToList();
      });
    }
    
    public SelectTourney(tourney : TourneyShortInfo) : void
    {
      server.GetTourney(tourney.Id, r => {
        Selected = r;
      });
    }
    
    public AddTourney() : void
    {
      Selected = Tourney();
    }
    
    [Html]
    public View() : string
    {
      <#
        <div class="tourney-list-container">
          <span click="$AddTourney">add</span>
          <h2>Pinned</h2>
          <ul class="tourney-list">
            <li $foreach(t in PinnedTourneys) 
                click="$SelectTourney"
                class="btn btn-info">$(t.Name)</li>
          </ul>
          <h2>Rest</h2>
          <ul class="tourney-list">
            <li $foreach(t in NormalTourneys) 
                click="$SelectTourney"
                class="btn btn-info">$(t.Name)</li>
          </ul>
        </div>
      #>
    }
        
    public class Server
    { 
      public Recent50() : IEnumerable[TourneyShortInfo]
      {
        Redis.Exec(_.GetRecent50());
      }
      
      public GetTourney(id : long) : Tourney
      {
        Redis.Exec(_.GetTourneyById(id));
      }
    }
  }
}
