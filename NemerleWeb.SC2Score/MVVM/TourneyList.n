using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using NemerleWeb.Macros;

namespace NemerleWeb.SC2Score
{ 
  [Unit]
  public class TourneyList
  {
    public PinnedTourneys : List[TourneyShortInfo] { get; set; }
    public NormalTourneys : List[TourneyShortInfo] { get; set; }
    public Selected : Tourney { get; set; }
    
    public this()
    {
      UpdateTourneyList();
    }
    
    public UpdateTourneyList() : void 
    {
      server.Recent50(result => {
        def (pinned, recent) = result;
        PinnedTourneys = pinned.ToList();
        NormalTourneys = recent.ToList();
      });
    }
    
    public SelectTourney(tourney : TourneyShortInfo) : void
    {
      server.GetTourney(tourney.Id, result => {
        Selected = result;
        Selected.SubscribeOnUpdate(() => UpdateTourneyList());
        Selected.IsPinned = PinnedTourneys.Any(p => p.Id == Selected.Id);
      });
    }
    
    public AddTourney() : void
    {
      Selected = Tourney();
      Selected.IsBeingEdited = true;
      Selected.SubscribeOnUpdate(() => UpdateTourneyList());
    }
    
    [Html]
    public View() : string
    {
      <#
        <div class="tourney-list-container">
          <span click="$AddTourney">add</span>
          <h2>Pinned</h2>
          <ul class="tourney-list">
            <li $foreach(t in PinnedTourneys) 
                click="$SelectTourney"
                class="btn btn-info">$(t.Name)</li>
          </ul>
          <h2>Rest</h2>
          <ul class="tourney-list">
            <li $foreach(t in NormalTourneys) 
                click="$SelectTourney"
                class="btn btn-info">$(t.Name)</li>
          </ul>
        </div>
      #>
    }
        
    public class Server
    { 
      public Recent50() : (IEnumerable[TourneyShortInfo] * IEnumerable[TourneyShortInfo])
      {
        Redis.Exec(_.GetRecent50());
      }
      
      public GetTourney(id : long) : Tourney
      {
        Redis.Exec(_.GetTourneyById(id));
      }
    }
  }
}
