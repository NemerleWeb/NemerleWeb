using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.SC2Score
{
  [Unit]
  public class Tourney
  {
    public Id : long { get; set; }
    public Name : string { get; set; };
    public Source : string { get; set; };
    public FromDate : DateTime? { get; set; };
    public ToDate : DateTime? { get; set; };
    
    private mutable _onSave : Action;
    private mutable IsBeingEdited : bool;
    
    private mutable _rendered : string;
    private Rendered : string {
      get
      {
        when(_rendered == null)
          RenderMarkdown();
        _rendered;
      }
    };    
    
    public this() {}    
    public this(name : string)
    {
      Name = name;      
      Source = <# This is a test source #>;      
    }
    
    public RenderMarkdown() : void
    {
      when(Source != null)
        _rendered = callJs.[string]("markdown.toHTML", Source);
    }
    
    public SourceChanged() : void
    {
      RenderMarkdown();
    }
    
    public Edit() : void
    {
      IsBeingEdited = true;
    }
    
    public Save() : void
    {
      server.Save(this, _ => {
        IsBeingEdited = false;
        _onSave();
      });
    }
    
    public SubscribeOnSave(action : Action) : void
    {
      _onSave = action;
    }
    
    [Html]
    public EditorView() : string
    {
      <#
        <div class="editor">
          <div $when(IsBeingEdited)>
            <input type="text" value="$Name" />
            <textarea event-keyup="$SourceChanged">$Source</textarea>
            <input type="text" value="$FromDate" />
            <input type="text" value="$ToDate" />
            <button click="$Save" class="btn btn-info">Save</button>
          </div>
          <div $when(!IsBeingEdited)>
            <button click="$Edit">edit</button>
          </div>
        </div>
      #>
    }
    
    [Html]
    public RenderedView() : string
    {
      <#<div class="rendered-tourney" html="$Rendered"></div>#>
    }
    
    public class Server
    {
      public Save(tourney : Tourney) : long
      {
        if(tourney.Id == 0)
          Redis.Exec(_.AddTourney(tourney));
        else
          Redis.Exec(_.SaveTourney(tourney));
      }
    }
  }  
}
