using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.SC2Score
{
  [Unit]
  public class Chat
  {
    [Record][Unit]
    public class Message 
    {
      public Author : string { get; set; }
      public Text : string { get; set; }
    }
    
    public CurrentUser : User { get; set; }
    public CurrentMessage : string { get; set; }
    public Messages : List[Message] { get; set; }    
    public MessagesScroll : int { get; set; }
    
    public this(user : User)
    {
      CurrentUser = user;
    }
    
    public ReceiveMessage(userName : string, message : string) : void
    {      
      Messages.Add(Message(userName, message));
      MessagesScroll = Messages.Count() * 100;
      when(Messages.Count() > 50)
        _ = Messages.Remove(Messages[0]);
    }
    
    public Send() : void
    {
      when(!string.IsNullOrEmpty(CurrentMessage))
        server.NewMessage(CurrentUser.Name, CurrentMessage, _result => {});
      CurrentMessage = "";
    }
    
    public MessageKeypress(e : EventData) : void
    {
      when(e.which == 13) {
        Send();
      }
    }
    
    public class Server
    {
      [SignalR]
      public NewMessage(userName : string, message : string) : string
      {
        broadcast client.ReceiveMessage(userName, message);
        "";
      }
    }
    
    [Html]
    public View() : string
    {
      <#
        <div class="chat-container">
          <div class="messages" attr-scrollTop="$MessagesScroll">
            <div class="messages-container">
              <div $foreach(m in Messages) class="message">
                <span class="message-author">$(m.Author)</span>: 
                <span class="message-text">$(m.Text)</span>
              </div>
            </div>
          </div>
          <div class="chat-new-message">            
            <input value="$CurrentMessage" event-keypress="$MessageKeypress" />
            <button click="$Send" disable="$(string.IsNullOrEmpty(CurrentMessage))" class="btn btn-info">Send</button>
          </div>
        </div>
      #>
    }
  }
}
