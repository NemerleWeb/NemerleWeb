using System.Collections.Generic;
using System.Linq;
using System.Text;
using Nemerle;
using NemerleWeb;
using System.Collections.Concurrent;

namespace NemerleWeb.Samples
{
  [Unit, Record]  
  class Sample
  {
    public Name : string { get; set; }
    public SourceFiles : list[string] { get; set; }
    public Source : string { get; set; }
    public Instance : object { get; set; }
  }
  
  [Unit]
  class SamplesPage
  {      
    SelectedSample : Sample { get; set; }
    
    Samples : List[Sample] { get; set; }
    KnockoutSamples : List[Sample] { get; set; }
    PegSamples : List[Sample] { get; set; }
    AllSamples : array[Sample] 
    {
      get
      {
        Samples.Concat(KnockoutSamples).Concat(PegSamples).ToArray()
      }
    }
    
    NameList : NameListPage { get; set; }
    NameListCSharp : NameListCSharpPage { get; set; }
    CompList : CompListPage { get; set; }
    Signal : SignalPage { get; set; }
    Variants : VariantsAndMatchingPage { get; set; }    
    Introduction : IntroductionPage { get; set; }
    ListsAndCollections : ListsAndCollectionsPage { get; set; }
    LoadingAndSaving : LoadingAndSavingPage { get; set; }
    SinglePageApps : SPAPage { get; set; }
    
    Calculator : CalculatorPage { get; set; }
    Json : JsonPage { get; set; }
    JS : JSPage { get; set; }
    TypeScript : TypeScriptInterfacePage { get; set; }
    
    Sources : List[string * string] { get; set; }
    ThisPageSource : string { get; set; }
        
    public this()
    {
      NameList = NameListPage();
      NameListCSharp = NameListCSharpPage();
      CompList = CompListPage();
      Signal = SignalPage();
      Variants = VariantsAndMatchingPage();
      Introduction = IntroductionPage("John", "Silver");
      ListsAndCollections = ListsAndCollectionsPage();
      LoadingAndSaving = LoadingAndSavingPage();
      SinglePageApps = SPAPage();
      Calculator = CalculatorPage();
      Json = JsonPage();
      JS = JSPage();
      TypeScript = TypeScriptInterfacePage();
            
      Samples = 
      [
        Sample("List of names", ["MVVM/NameListPage.n"], "", NameList),  // in tname template(_.NameList) second parameter is View by default
        Sample("List of names C#", ["MVVM/NameListCSharpPage.cs"], "", NameListCSharp), 
        Sample("Computation Expressions", ["MVVM/CompListPage.n"], "", CompList), 
        Sample("Chat using SignalR", ["MVVM/SignalPage.n"], "", Signal),
        Sample("Variants and Pattern Matching", ["MVVM/VariantsAndMatchingPage.n"], "", Variants)
      ].ToList();
      
      KnockoutSamples =
      [
        Sample("Introduction", ["MVVM/Knockout/Tutorial_1_Introduction.n"], "", Introduction), 
        Sample("Lists and collections", ["MVVM/Knockout/Tutorial_2_ListsAndCollections.n"], "", ListsAndCollections), 
        Sample("Single page applications", ["MVVM/Knockout/Tutorial_3_SinglePageApps.n"], "", SinglePageApps), 
        Sample("Loading and saving", ["MVVM/Knockout/Tutorial_5_LoadingAndSaving.n"], "", LoadingAndSaving)
      ].ToList();
      
      PegSamples =
      [
        Sample("Calculator", ["MVVM/Peg/CalculatorParser/CalcParser.n", "MVVM/Peg/CalculatorPage.n"], "", Calculator),
        Sample("Json", ["MVVM/Peg/JsonParser/JsonParser.n", "MVVM/Peg/JsonPage.n", "MVVM/Peg/JsonParser/Ast.n", "MVVM/Peg/JsonParser/JsonUtils.n"], "", Json),
        Sample("JavaScript", ["MVVM/Peg/JSParser/JSParser.n", "MVVM/Peg/JSPage.n", "MVVM/Peg/JSParser/Ast.n"], "", JS),
        Sample("TypeScript", ["MVVM/Peg/TypeScriptInterfaceParser/TypeScriptInterfaceParser.n", "MVVM/Peg/TypeScriptInterfacePage.n"], "", TypeScript),
      ].ToList();
                 
      SelectedSample = Samples[2];
            
      LoadSamples(Samples);
      LoadSamples(KnockoutSamples);
      LoadSamples(PegSamples);
            
      server.LoadSourceFor("MVVM/Samples.n", source =>
      {
        ThisPageSource = source;
        js prettyPrint();
      });
    }
    
    LoadSamples(samples : List[Sample]) : void
    {
      for(mutable i = 0; i < samples.Count; i++)      
        LoadSample(samples, i)
    }
    
    LoadSample(samples : List[Sample], index : int) : void
    {
      server.LoadSourceForList(samples[index].SourceFiles, source =>
      {          
        samples[index].Source = source;
        js prettyPrint();
      });  
    }
    
    SelectSample(sample : Sample) : void 
    {
      SelectedSample = sample;
      js <# nweb.invalidate() #>;
      js prettyPrint();
    }
    
    public class Server
    {
      public static _lastMessages : ConcurrentQueue[Sample] = ConcurrentQueue.[Sample]();
      
      public LoadSourceFor(sample : string) : string
      {
        Helpers.LoadSourceFor(sample);
      }
      
      public LoadSourceForList(samples : IEnumerable[string]) : string
      {
        $<#..$(samples; "\n---------------------------------\n"; Helpers.LoadSourceFor)#>
      }
    }
    
    [Html]
    public View() : string
    {
      <# 
        <div class="container">
          <ul class="samples-menu">
            <li class="header">Samples</li>
            <li $foreach(s in Samples) css-selected="$(SelectedSample.Name == s.Name)"><span click="$SelectSample">$(s.Name)</span></li>
            <li class="split"></li>
            <li class="header">KnockoutJs tutorials</li>
            <li $foreach(s in KnockoutSamples) css-selected="$(SelectedSample.Name == s.Name)"><span click="$SelectSample">$(s.Name)</span></li>
            <li class="split"></li>
            <li class="header">Peg Samples</li>
            <li $foreach(s in PegSamples) css-selected="$(SelectedSample.Name == s.Name)"><span click="$SelectSample">$(s.Name)</span></li>
          </ul>
          <div $foreach(s in AllSamples)>
            <div $when(SelectedSample.Name == s.Name) class="current-sample">
              <div template="$(template(s.Instance))"></div>
              <h2>Source</h2>
              <pre class="prettyprint lang-n prettyprinted">$(s.Source)</pre>
            </div>
          </div>
          <h2>Main page source</h2>
          <pre class="prettyprint lang-n prettyprinted">$ThisPageSource</pre>
        </div>
      #>
    }    
  }
}
