using System.Collections.Generic;
using System.Linq;
using System.Text;
using Nemerle;
using NemerleWeb;
using System.Collections.Concurrent;

namespace NemerleWeb.Samples
{
  [Unit, Record]  
  class Sample
  {
    mutable public Name : string;// { get; set; }
    mutable public SourceFiles : list[string];// { get; set; }
    mutable public Source : string;// { get; set; }
    mutable public Instance : object;// { get; set; }
  }
  
  [Unit]
  class SamplesPage
  {      
    mutable SelectedSample : Sample;// { get; set; }
    
    mutable Samples : List[Sample];// { get; set; }
    mutable KnockoutSamples : List[Sample];// { get; set; }
    mutable PegSamples : List[Sample];// { get; set; }
    AllSamples : array[Sample] 
    {
      get
      {
        Samples.Concat(KnockoutSamples).Concat(PegSamples).ToArray()
      }
    }
    
    mutable NameList : NameListPage;// { get; set; }
    mutable NameListCSharp : NameListCSharpPage;// { get; set; }
    mutable CompList : CompListPage;// { get; set; }
    mutable Signal : SignalPage;// { get; set; }
    mutable Variants : VariantsAndMatchingPage;// { get; set; }    
    mutable Introduction : IntroductionPage;// { get; set; }
    mutable ListsAndCollections : ListsAndCollectionsPage;// { get; set; }
    mutable LoadingAndSaving : LoadingAndSavingPage;// { get; set; }
    mutable SinglePageApps : SPAPage;// { get; set; }
    
    mutable Calculator : CalculatorPage;// { get; set; }
    
    mutable Sources : List[string * string];// { get; set; }
    mutable ThisPageSource : string;// { get; set; }
        
    public this()
    {
      NameList = NameListPage();
      NameListCSharp = NameListCSharpPage();
      CompList = CompListPage();
      Signal = SignalPage();
      Variants = VariantsAndMatchingPage();
      Introduction = IntroductionPage("John", "Silver");
      ListsAndCollections = ListsAndCollectionsPage();
      LoadingAndSaving = LoadingAndSavingPage();
      SinglePageApps = SPAPage();
      Calculator = CalculatorPage();
            
      Samples = 
      [
        Sample("List of names", ["MVVM/NameListPage.n"], "", NameList),  // in tname template(_.NameList) second parameter is View by default
        Sample("List of names C#", ["MVVM/NameListCSharpPage.cs", "MVVM/NameListCSharpPage.View.n"], "", NameListCSharp), 
        Sample("Computation Expressions", ["MVVM/CompListPage.n"], "", CompList), 
        Sample("Chat using SignalR", ["MVVM/SignalPage.n"], "", Signal),
        Sample("Variants and Pattern Matching", ["MVVM/VariantsAndMatchingPage.n"], "", Variants)
      ].ToList();
      
      KnockoutSamples =
      [
        Sample("Introduction", ["MVVM/Knockout/Tutorial_1_Introduction.n"], "", Introduction), 
        Sample("Lists and collections", ["MVVM/Knockout/Tutorial_2_ListsAndCollections.n"], "", ListsAndCollections), 
        Sample("Single page applications", ["MVVM/Knockout/Tutorial_3_SinglePageApps.n"], "", SinglePageApps), 
        Sample("Loading and saving", ["MVVM/Knockout/Tutorial_5_LoadingAndSaving.n"], "", LoadingAndSaving)
      ].ToList();
      
      PegSamples =
      [
        Sample("Calculator", ["MVVM/Peg/CalculatorPage.n"], "", Calculator),
      ].ToList();
                 
      SelectedSample = Samples[2];
            
      LoadSamples(Samples);
      LoadSamples(KnockoutSamples);
      LoadSamples(PegSamples);
            
      server.LoadSourceFor("MVVM/Samples.n", source =>
      {
        ThisPageSource = source;
        js prettyPrint();
      });
    }
    
    LoadSamples(samples : List[Sample]) : void
    {
      for(mutable i = 0; i < samples.Count; i++)      
        LoadSample(samples, i)      
    }
    
    LoadSample(samples : List[Sample], index : int) : void
    {
      server.LoadSourceForList(samples[index].SourceFiles, source =>
      {          
        samples[index].Source = source;
        js prettyPrint();
      });  
    }
    
    SelectSample(sample : Sample) : void 
    {
      SelectedSample = sample;
      js prettyPrint();
    }
    
    public class Server
    {
      public static _lastMessages : ConcurrentQueue[Sample] = ConcurrentQueue.[Sample]();
      
      public LoadSourceFor(sample : string) : string
      {
        Helpers.LoadSourceFor(sample);
      }
      
      public LoadSourceForList(samples : IEnumerable[string]) : string
      {
        $<#..$(samples; "\n"; Helpers.LoadSourceFor)#>
      }
    }
    
    [Html]
    public View() : string
    {
      <# 
        <div class="container">
          <ul class="samples-menu">
            <li class="header">Samples</li>
            <li $foreach(s in Samples) css-selected="$(SelectedSample.Name == s.Name)"><span click="$SelectSample">$(s.Name)</span></li>
            <li class="split"></li>
            <li class="header">KnockoutJs tutorials</li>
            <li $foreach(s in KnockoutSamples) css-selected="$(SelectedSample.Name == s.Name)"><span click="$SelectSample">$(s.Name)</span></li>
            <li class="split"></li>
            <li class="header">Peg Samples</li>
            <li $foreach(s in PegSamples) css-selected="$(SelectedSample.Name == s.Name)"><span click="$SelectSample">$(s.Name)</span></li>
          </ul>
          <div $foreach(s in AllSamples)>
            <div $when(SelectedSample.Name == s.Name) class="current-sample">
              <div template="$(template(s.Instance))"></div>
              <h2>Source</h2>
              <pre class="prettyprint lang-n prettyprinted">$(s.Source)</pre>
            </div>
          </div>
          <h2>Main page source</h2>
          <pre class="prettyprint lang-n prettyprinted">$ThisPageSource</pre>
        </div>
      #>
    }    
  }
}
