using NemerleWeb;
using Nemerle.Extensions;
using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.Samples
{
  [Unit]
  public class TSDeclarationPage
  {
    _parser : TypeScriptSample.TSDeclarationParser;
    
    public this()
    {
       _parser = TypeScriptSample.TSDeclarationParser();
    }
         
    ParserOutput : string
    {
      get
      {
        if (!string.IsNullOrEmpty(ParserInput))
          if (_parser.Parse(ParserInput) is Some(res))
            res.ToString()
          else
            "Incorrect input"
        else
          "";
      }
    }
     
    public ParserInput : string { get; set; }
    
    [Html]
    public View() : string
    {
      <#
        <div>
          <span>This is demonstration of TypeScript interface parser</span><br />
          <div>Input:</div>
          <div class="parser-input">
            <textarea rows="6" cols="50" value="$ParserInput"/>
          </div>
          <div>AST:</div>
          <div class="output">
            <textarea rows="6" cols="50" value="$ParserOutput"/>
          </div>
        </div>
      #>
    }    
  }
}
