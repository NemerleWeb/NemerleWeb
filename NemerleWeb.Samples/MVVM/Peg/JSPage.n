using NemerleWeb;
using Nemerle.Extensions;
using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb.Samples
{
  [Unit]
  public class JSPage
  {
    _parser : JSEngine.JSParser = JSEngine.JSParser();
         
    ParserOutput : string
    {
      get
      {
        if (!string.IsNullOrEmpty(ParserInput))
          if (_parser.Parse(ParserInput) is Some(res))
            $<#..$(res; "\n"; _.ToString())#>
          else
            "Incorrect input"
        else
          "";
      }
    }
     
    public ParserInput : string { get; set; }
    
    [Html]
    public View() : string
    {
      <#
        <div>
          <span>This is demonstration of JavaScript parser</span><br />
          <span>When you write JavaScript code, the parser parses it and creates AST.</span><br />
          <span>Afterwards this AST is converted to human readable string</span>
          <div class="output">
            <span class="output-text">$ParserOutput</span> 
          </div>
          <div class="parser-input">
            <textarea rows="6" cols="50" value="$ParserInput"/>
          </div>
        </div>
      #>
    }    
  }
}
