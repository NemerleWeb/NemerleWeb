using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using NemerleWeb;

namespace NemerleWeb.Samples
{
  [Unit]
  public variant Color
  {
    | Red
    | Green
    | Blue
    | Other
    {
      R : int;
      G : int;
      B : int;      
    }
    
    public Style : string
    {
      get 
      {
        match(this) 
        {
        | Red   => "rgb(255, 0, 0)"
        | Green => "rgb(0, 255, 0)"
        | Blue  => "rgb(0, 0, 255)"
        | Other(r, g, b) => $"rgb($r, $g, $b)"
        | _ => "rgb(0,0,0)"
        }
      }
    }
  }
  
  [Unit]
  public class VariantsAndMatchingPage
  { 
    AvailableColors : list[(int * string)] { get { [(0, "Red"), (1, "Green"), (2, "Blue"), (3, "Other")] }  }
    mutable CurrentColorId : int;// { get; set; }
    OtherColor : Color.Other;
        
    public CurrentColor : Color
    {
      get
      {
        match(CurrentColorId)
        {
        | 0 => Color.Red()
        | 1 => Color.Green()
        | 2 => Color.Blue()
        | 3 => OtherColor
        | _ => Color.Red();
        }
      }
    }      
    
    public this()
    {
      OtherColor = Color.Other(0, 0, 0);
    }
    
    [Html]
    public View() : string
    {
      <#
        <div>
          <div $when(CurrentColor != null) class="color-box" style-background-color="$(CurrentColor.Style)"></div>
          <select value="$CurrentColorId">
            <option $foreach(color in AvailableColors) value="$(color[0])" text="$(color[1])" />
          </select>
          <span $when(CurrentColor is Color.Other)> 
            <span>$(OtherColor.R)</span>
            <span>$(OtherColor.G)</span>
            <span>$(OtherColor.B)</span>
          </span>
        </div>
      #>
    }
  }
}
