using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using NemerleWeb;

using System;
using System.Reflection;
using System.Collections.Generic;
using System.Linq;
using System.Runtime.Serialization;

namespace NemerleWeb
{
  public module NWebData
  {
    public UnitDefinitions : Dictionary[string, string] = Dictionary.[string, string]();
    public SignalDefinitions : Dictionary[string, string] = Dictionary();
    public UnitTemplates : Dictionary[string, Dictionary[string, string]] = Dictionary.[string, Dictionary[string, string]]();
    public UnitLambdas : Dictionary[string, string] = Dictionary.[string, string]();
    
    this() 
    {
      def datas = AppDomain.CurrentDomain
                           .GetAssemblies()
                           .SelectMany(a => a.GetTypes()
                                             .Where(t => t.Name.EndsWith("NWebDataPerAssembly")));
      foreach(dataType in datas) {
        def instance = Activator.CreateInstance(dataType, array[], array[]) :> NWebDataInstance;
        
        foreach(u in instance.UnitDefinitions)
          UnitDefinitions.Add(u.Key, u.Value);
          
        foreach(s in instance.SignalDefinitions)
          SignalDefinitions.Add(s.Key, s.Value);
          
        foreach(u in instance.UnitTemplates)
           UnitTemplates.Add(u.Key, u.Value);

        foreach(u in instance.UnitLambdas)
          UnitLambdas.Add(u.Key, u.Value);
      }
    }         
    
    public GetAllScriptNames() : IEnumerable[string]
    {
      UnitDefinitions.Keys
    }
    
    public GetScript(unitName : string) : string
    {
      mutable unitDef;
      when(!UnitDefinitions.TryGetValue(unitName, out unitDef))
        throw InvalidOperationException($"Unit $unitName wasn't defined");
              
      unitDef.Replace("{{{SignalR}}}", SignalDefinitions.GetOrAdd(unitName, () => ""));
    }
    
    public GetLambdasScript(unitName : string) : string
    {
      mutable unitDef;
      when(!UnitLambdas.TryGetValue(unitName, out unitDef))
        throw InvalidOperationException($"Lambdas for $unitName weren't defined");
      unitDef
    }
    
    public GetTemplatesScripts(unitName : string) : string
    {
      mutable templates;
      if(UnitTemplates.TryGetValue(unitName, out templates)) {
        def templateScripts = templates.Map(templateKvp => {
            $<#
            <script type="text/html" id="$(templateKvp.Key)">
            $(templateKvp.Value)
            </script>
            #>
        });      
        $"..$templateScripts"
      } else {
        ""
      }      
    }
  }
  
  public class NWebDataInstance
  {
    public UnitDefinitions : Dictionary[string, string] = Dictionary.[string, string]();
    public SignalDefinitions : Dictionary[string, string] = Dictionary();
    public UnitTemplates : Dictionary[string, Dictionary[string, string]] = Dictionary.[string, Dictionary[string, string]]();
    public UnitLambdas : Dictionary[string, string] = Dictionary.[string, string]();
             
    public AddUnitMainDefinition(unitName : string, definition : string) : void
    {
      UnitDefinitions.Add(unitName, definition);
    }
    
    public AddUnitLambdas(unitName : string, lambdas : string) : void
    {
      UnitLambdas.Add(unitName, lambdas);
    }
    
    public AddUnitTemplate(unitName : string, templateName : string, template : string) : void
    {
      mutable templateDict;
      when(!UnitTemplates.TryGetValue(unitName, out templateDict)) {
        templateDict = Dictionary.[string, string]();
        UnitTemplates.Add(unitName, templateDict);
      }
      templateDict.Add(templateName, template);
    }
    
    public AddUnitSignalRDefinition(unitName : string, signalr : string) : void
    {
      SignalDefinitions.Add(unitName, signalr);      
    }
  }
}
