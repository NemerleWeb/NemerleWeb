using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{ 
  internal module StringExtensions
  {
    public static FirstCharacterToLower([NotNull] this str : string) : string 
    {
      if (str.Length == 0 || Char.IsLower(str, 0))
        str
      else
        Char.ToLowerInvariant(str[0]).ToString() + str.Substring(1);
    }
  }
  
  // Mini lifting
  internal module JsAstExtensions
  {
    public static JsMethodCall(this self : JsAst, [NotNull] method : string, [NotNull] parms : list[JsAst]) : JsAst
    {
      JsAst.Call(JsAst.MethodRef(self, method.FirstCharacterToLower(), None()), parms)
    }
    
    public static MethodCall(this self : JsAst, [NotNull] method : IMember, [NotNull] parms : list[JsAst]) : JsAst
    {
      JsAst.Call(
        JsAst.MethodRef(
          self,
          method.Name,
          JsAstHelper.MethodParameters(method :> IMethod)),
       parms)
    }
    
    public static ExternalMethodCall(this self : JsAst, [NotNull] method : string, [NotNull] parms : list[JsAst]) : JsAst
    {
      JsAst.Call(JsAst.MethodRef(self, method, None()), parms)
    }
    
    public static JsStaticCall(this type : string, [NotNull] method : IMember, [NotNull] parms : list[JsAst]) : JsAst
    {
      JsAst.Call(JsAst.StaticMethodRef(type, method.Name.FirstCharacterToLower(), None()), parms)
    }
    
    public static JsStaticCall(this type : string, [NotNull] methodName : string, [NotNull] parms : list[JsAst]) : JsAst
    {
      JsAst.Call(JsAst.StaticMethodRef(type, methodName, None()), parms)
    }
    
    public static StaticCall(this type : string, [NotNull] method : IMember, [NotNull] parms : list[JsAst]) : JsAst
    {
      JsAst.Call(
        JsAst.StaticMethodRef(
          type,
          method.Name,
          JsAstHelper.MethodParameters(method :> IMethod)),
        parms)
    }
    
    public static Field(this self : JsAst, [NotNull] field : string) : JsAst
    {
      JsAst.FieldRef(self, field)
    }
  }
  
  public module JsKnownMembers
  {
    static this()
    {
      JsKnownMembersBase.Register(); 
      JsKnownMembersOperators.Register(); 
      JsKnownMembersDelegate.Register();
      JsKnownMembersObject.Register();
      JsKnownMembersString.Register();
      JsKnownMembersIDisposable.Register();
      JsKnownMembersConsole.Register();
      JsKnownMembersConvert.Register();
      
      JsKnownMembersSCGList.Register();
      JsKnownMembersIEnumerable.Register();
      JsKnownMembersEnumerator.Register();
      
      JsKnownMembersLinqEnumerable.Register();
      
      JsKnownMembersNemerleList.Register();
      JsKnownMembersNemerleNCollectionsExtensions.Register();
      JsKnownMembersEventsDSL.Register();
      JsKnownMembersPage.Register();
      
      JsKnownMembersIntegers.Register();
      JsKnownMembersFloats.Register();
      JsKnownMembersArray.Register();
      
      JsKnownMembersMath.Register();
      JsKnownMembersNemerleOption.Register();
      JsKnownMembersTextStringBuilder.Register();
      JsKnownMembersGlobalizationCutlureInfo.Register();
      JsKnownMembersCharEnumerator.Register();
      JsKnownMembersNemerleCollectionsNList.Register();
    }
    
    public Convert(
      [NotNull] call : TExpr.Call,
      [NotNull] builder : TExpr * option[ParmKind] -> JsAst) : JsAst
    {
      def builder'(expr) { builder(expr, None()) }
      def paramBuilder(p) { builder(p.expr, Some(p.kind)) }
      
      def isRefOut(p)
      {
        match (p.kind)
        {
        | ParmKind.Ref | ParmKind.Out => true
        | _ => false
        }
      }
      
      // Pack ref/out
      // x = { value: x };
      def packRefOut = call.parms
        .Where(isRefOut)
        .Select(p => builder'(p.expr))
        .Where(expr => !(expr is JsAst.ParamRef()))
        .Select(expr => JsAst.Pack(expr));
      
      def callAst =
        match (call)
        {
        | Call(MethodRef(obj = Call(MethodRef(meth = serverMeth, obj = obj), _, _), meth = meth), parms, _)
          when serverMeth.Name == "get_server" && obj is TExpr.This =>
            def parameters =
              if (parms.Length > 0) parms.Zip(meth.GetParameters()).Take(parms.Length - 1).Map((cp, mp) => (mp.name, builder'(cp.expr)))
              else [];
            def callback = if(parms.Length > 0) builder'(parms.Last().expr) else JsAst.Void();
            match(callback)
            {
            | Seq(DefValue as dv :: cb :: []) =>
              JsAst.Seq([dv, JsAst.ServerCall(meth.Name, serverMeth.DeclaringType.Name, parameters, cb)])
            | _ => JsAst.ServerCall(meth.Name, serverMeth.DeclaringType.Name, parameters, callback)
            }
        | Call(MethodRef(_, meth, _, _), [modelParm, viewParm], _) when meth.Name == "GetTemplateName" =>             
          match(modelParm, viewParm)
          {
          | (Parm(expr = TExpr.Literal(Literal.String(model))),
              Parm(expr = TExpr.Literal(Literal.String(view)))) =>
            JsAst.Code($"nweb.utils.getTemplateName(this.$model, '$view')")
          | _ => Message.FatalError("Invalid GetTemplateName call, use 'name' macro")
          }
        | Call(MethodRef(obj = o, meth = meth), parms, _) =>
          if(o.Type.TypeInfo != null)
            ConvertMethod(o.Type.TypeInfo, builder'(o), meth, parms.Map(paramBuilder));
          else
            ConvertFixedTypeMethod(o.Type.FixedValue, builder'(o), meth, parms.Map(paramBuilder))
        | Call(StaticRef(from = fromType, mem = meth) as left, parms, _) =>
            ConvertMethod(fromType.TypeInfo, builder'(left), meth, parms.Map(paramBuilder))
        | Call(OpCode as op, parms, _) => JsAst.Call(builder'(op), parms.Map(paramBuilder))
        | Call(Base(baseCtor), parms, _) =>
          // Call base constructor for Unit bases
          def declaringType = baseCtor.DeclaringType;
          if (declaringType.GetSuperTypes().Any(_.TryRequire(JsClass.IPageType)))
          {
            JsAst.Call(
              JsAst.ConstructorRef(
                JsClass.GetProperName(declaringType),
                Some(baseCtor.Header.Parameters)),
              parms.Map(paramBuilder))
          }
          else
            JsAst.Void()
        | Call(Delayed, _, _) => JsAst.Void()
        | Call(s, _, _) => Message.Error(s.Location, $"Unsupported call to $s"); JsAst.Void()          
        }
      
      // Unpack ref/out
      // x = x.value
      def unpackRefOut = call.parms
        .Where(isRefOut)
        .Select(p => builder'(p.expr))
        .Where(expr => !(expr is JsAst.ParamRef()))
        .Select(expr => JsAst.Unpack(expr));
      
      if(packRefOut.Any())
        JsAst.Seq([JsAst.Seq(packRefOut.NToList()), callAst, JsAst.Seq(unpackRefOut.NToList())])
      else
        callAst
    }
    
    #region ConvertMethod
    
    internal type ConvertTypeHandler = JsAst * IMember * list[JsAst] -> option[JsAst];
    
    // typeof().FullName -> Handler
    private static _convertTypeHandlers : Dictionary[string, ConvertTypeHandler] = Dictionary();    
    private static _convertBaseTypeHandlers : Dictionary[string, ConvertTypeHandler] = Dictionary();
    
    internal type ConvertMethodHandler = TypeInfo * JsAst * IMember * list[JsAst] -> option[JsAst];
    
    // memberName -> Handler
    private static _convertMethodHandlers : Dictionary[string, ConvertMethodHandler] = Dictionary();
    
    // Default memberName -> Handler
    private static _convertDefaultMemberHandlers : Dictionary[string, ConvertMethodHandler] = Dictionary();

    /// <summary>
    /// Convert method.
    /// Note: type can be null.
    /// </summary>
    public ConvertMethod(
      typeInfo : TypeInfo,
      [NotNull] left : JsAst,
      [NotNull] member : IMember,
      [NotNull] parms : list[JsAst]) : JsAst
    {
      // First try convert member by name
      def memberName = member.Name;
      mutable memberHandler;
      when (_convertMethodHandlers.TryGetValue(memberName, out memberHandler))
        when (memberHandler(typeInfo, left, member, parms) is Some(ast))
          return ast;
      
      // If no member handler found, try method handler
      def typeName = TypeName(typeInfo);
      mutable typeHandler;
      when (_convertTypeHandlers.TryGetValue(typeName, out typeHandler))
        when (typeHandler(left, member, parms) is Some(ast))
          return ast;
          
      // Try using just FullName if SystemType
      when (typeInfo?.SystemType != null)
        when (_convertTypeHandlers.TryGetValue(typeInfo.FullName, out typeHandler))
          when (typeHandler(left, member, parms) is Some(ast))
            return ast;
            
      // Try looking for base types
      foreach(subType in GetSubTypeList(typeInfo))
        when (subType?.SystemType != null)
          when (_convertBaseTypeHandlers.TryGetValue(subType.SystemType.FullName, out typeHandler))
            when (typeHandler(left, member, parms) is Some(ast))
              return ast;
            
      when(typeInfo?.IsCustomAttributeDefined("NemerleWeb.JsAstAttribute"))
      {
        if(member.IsConstructor)
          return JsAst.Call(JsAst.Constructor(typeInfo.Name, None()), parms);
        else if (left is JsAst.StaticMethodRef)
          return typeInfo.Name.StaticCall(member, parms);
        else
          return left.MethodCall(member, parms);
      }
              
      // Nothing found, try default dictionary
      mutable defaultMemberHandler;
      when (_convertDefaultMemberHandlers.TryGetValue(memberName, out defaultMemberHandler))
        when (defaultMemberHandler(typeInfo, left, member, parms) is Some(ast))
          return ast;
          
      // Process low level naming
      DefaultConvertMethod(typeInfo, left, member, parms)
    }
    
    private ConvertFixedTypeMethod(type : FixedType, left : JsAst, member : IMember, parms : list[JsAst]) : JsAst
    {
      match(type)
      {
      | FixedType.Array => 
        match(JsKnownMembersArray.Handler(left, member, parms))
        {
        | Some(ast) => ast
        | None => JsAst.NotImpl(member.Name)
        }        
      | _ => left.MethodCall(member, parms)
      }
    }
    
    private DefaultConvertMethod(typeInfo : TypeInfo, left : JsAst, member : IMember, parms : list[JsAst]) : JsAst
    {
      if (left is StaticMethodRef())
        JsAst.Call(left, parms)
      else
      {
        when (typeInfo != null &&
          !(typeInfo.IsAbstract ||
            typeInfo.IsInterface ||
            typeInfo.GetSuperTypes().Any(t => t.TryRequire(JsClass.IPageType)) ||
            member.Name == "apply" ||
            member.Name == "apply_void"))
          Message.Error(
            member.Location,
            $"Invalid $(member.Name) with $(TypeName(typeInfo)) type, params: ..$parms not implemented in JS");
          
        left.MethodCall(member, parms)
      }
    }
    
    public RegisterType([NotNull] type : Type, [NotNull] handler : ConvertTypeHandler) : void
    {
      _convertTypeHandlers.Add(type.FullName, handler);
    }
    
    public RegisterType([NotNull] type : string, [NotNull] handler : ConvertTypeHandler) : void
    {
      _convertTypeHandlers.Add(type, handler);
    }
    
    public RegisterBaseType([NotNull] type : Type, [NotNull] handler : ConvertTypeHandler) : void
    {
      _convertBaseTypeHandlers.Add(type.FullName, handler);
    }
    
    public RegisterMember([NotNull] memberName : string, [NotNull] handler : ConvertMethodHandler) : void
    {
      _convertMethodHandlers.Add(memberName, handler);
    }
    
    public RegisterDefaultMember([NotNull] memberName : string, [NotNull] handler : ConvertMethodHandler) : void
    {
      _convertDefaultMemberHandlers.Add(memberName, handler);
    }
    
    public GetSubTypeList(type : TypeInfo) : list[TypeInfo]
    {
      match(type?.BaseType)
      {        
      | null => []
      | baseType => baseType :: GetSubTypeList(baseType)
      }
    }
    
    #endregion
      
    public ConvertField([NotNull] obj : TExpr, [NotNull] field : IField, [NotNull] builder : TExpr -> JsAst) : JsAst
    {
      if ((field.Name == "hd" || field.Name == "tl") &&
        field.DeclaringType.FrameworkTypeName == "Nemerle.Core.list+Cons")
        JsAst.Call(JsAst.MethodRef(builder(obj), field.Name, None()), [])        
      else
        JsAst.FieldRef(builder(obj), field.Name) 
    }
    
    #region Helper methods
        
    private InternalTypeName(type : TypeInfo) : string
    {
      if (type.SystemType != null) type.SystemType.FullName else type.FrameworkTypeName;
    }
    
    [Memoize]
    private TypeName(type : TypeInfo) : string
    {
      if (type != null) InternalTypeName(type) else "";
    }
    
    #endregion
  }
}
