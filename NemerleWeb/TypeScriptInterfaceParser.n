using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Peg;
using NemerleWeb;

using System;
using System.Collections.Generic;
using System.Linq;

namespace TypeScriptInterface
{
  
  [PegGrammar(Options = EmitDebugSources,
      start,
      grammar {
        newLine                     = "\r\n" / '\n' / '\r' / '\u2028' / '\u2029' / '\u0085';
        singleLineComment           = "//" (!('\n' / '\r') [Any])* newLine?;
        delimitedComment            = "/*" (!"*/" [Any])* "*/";
        comment                     = singleLineComment / delimitedComment;
        spaces : Void               = (' ' / '\t' / '\u000B' / '\u000C' / newLine / comment)*;
        letter                      = ([Lu, Ll, Lt, Lm, Lo, Nl] / '_' / '$');
        decimal_digit               = ['0'..'9'];
        hex_digit                   = ['0'..'9', 'A'..'F', 'a'..'f'];
        octal_digit                 = ['0'..'7'];

        SEMICOLON                   = ';' spaces;
        COLON                       = ':' spaces;
        DOT                         = '.' spaces;
        COMMA                       = ',' spaces;
        LPAREN                      = '(' spaces;
        RPAREN                      = ')' spaces;
        LBRACE                      = '[' spaces;
        RBRACE                      = ']' spaces;
        LFBRACE                     = '{' spaces;
        RFBRACE                     = '}' spaces;
        EQ                          = '=' spaces;
        QUEST                       = '?' spaces;
        
        identifierValue             = letter (letter / decimal_digit)*;
        identifier      : string    = identifierValue spaces;
        identifiers     : list[string] = identifier (COMMA identifiers)?;

        constructor : TypeScriptParameter     = "new" spaces LPAREN parms? RPAREN (COLON type)? SEMICOLON;
        dictionaryType : TypescriptType       = LFBRACE LBRACE identifier COLON spaces type RBRACE COLON type SEMICOLON RFBRACE;
        objectType : TypescriptType           = LFBRACE parmsSemicolon? RFBRACE;
        arrayType : TypescriptType            = nonArrayType "[]";
        normalType : TypescriptType           = identifier;
        lambdaType : TypescriptType           = LPAREN parms? RPAREN "=>" spaces type;
        nonArrayType : TypescriptType         = lambdaType / normalType / objectType / dictionaryType;
        type : TypescriptType                 = nonArrayType / arrayType;
        nonOptionalParam : TypeScriptParameter          = (identifier COLON type);
        optionalParam : TypeScriptParameter             = identifier QUEST COLON type;
        param : TypeScriptParameter                     = (constructor / nonOptionalParam / optionalParam);
        multipleParam : TypeScriptParameter             = "..." param;
        parms : list[TypeScriptParameter]               = (param/multipleParam) (COMMA parms)?;
        parmsSemicolon : list[TypeScriptParameter]      = param (SEMICOLON parmsSemicolon)? SEMICOLON?;
        callDeclaration : TypeScriptMember              = LPAREN parms? RPAREN COLON type SEMICOLON;
        indexCallDeclaration : TypeScriptMember         = LBRACE parms? RBRACE COLON type SEMICOLON;
        methodDeclaration : TypeScriptMember            = identifier QUEST? LPAREN parms? RPAREN (COLON type)? SEMICOLON;
        propertyDeclaration : TypeScriptMember          = identifier QUEST? COLON type SEMICOLON;
        memberDeclaration : TypeScriptMember            = (propertyDeclaration / methodDeclaration / callDeclaration / indexCallDeclaration);
        interfaceBlock : list[TypeScriptMember]         = LFBRACE memberDeclaration* RFBRACE;        
        // extends is reserved keyword in JS
        extends_ : list[string]                = "extends" spaces identifiers;
        _interface : TypeScriptGlobalDeclaration                = "interface" spaces identifier extends_? interfaceBlock;
        
        globalVarDeclaration : TypeScriptGlobalDeclaration = "declare" spaces "var" spaces identifier COLON type SEMICOLON;
        globalFunctionDeclaration : TypeScriptGlobalDeclaration = "declare" spaces "function " methodDeclaration;
        
        start : list[TypeScriptGlobalDeclaration] = spaces (_interface / globalVarDeclaration / globalFunctionDeclaration)*;
      }
    )]
  public class TypeScriptInterfaceParser
  {
    identifier(idValue : NToken) : string 
    {
      //System.Diagnostics.Debug.WriteLine("identifier: " + GetText(idValue));
      GetText(idValue)
    }
    
    constructor(_ : NToken, _ : NToken, lst : option[list[TypeScriptParameter]], _ : NToken, _ : option[(NToken * TypescriptType)], _ : NToken) : TypeScriptParameter
    {
      TypeScriptParameter("ctor", TypescriptType.Normal("void"), false, false)
    }    
    
    identifiers(id : string, tail : option[NToken * list[string]]) : list[string] 
    {
      //System.Diagnostics.Debug.WriteLine("identifiers");
      match(tail) {
      | Some((_, tail)) => id :: tail
      | _ => [id]
      }
    }
    
    dictionaryType(_ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, _keyname : string, _ : Nemerle.Peg.NToken, key : TypescriptType, _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, value : TypescriptType, _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken) : TypescriptType
    {
      TypescriptType.Dictionary(key, value)
    }
    
    objectType(_ : Nemerle.Peg.NToken, parms : option[list[TypeScriptParameter]], _ : Nemerle.Peg.NToken) : TypescriptType
    {
      TypescriptType.Object(if(parms is Some(parms)) parms else [])
    }
    
    arrayType(type : TypescriptType, _ : Nemerle.Peg.NToken) : TypescriptType
    {
      TypescriptType.Array(type)
    }

    normalType(name : string) : TypescriptType
    {
      TypescriptType.Normal(name)
    }

    lambdaType(_ : Nemerle.Peg.NToken, parms : option[list[TypeScriptParameter]], _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, returnType : TypescriptType) : TypescriptType    
    {
      TypescriptType.Lambda(parms, returnType)
    }

    nonOptionalParam(name : string, _ : NToken, type : TypescriptType) : TypeScriptParameter
    {
      //System.Diagnostics.Debug.WriteLine("nonOptionalParm");
      TypeScriptParameter(name, type, false, false)
    }
    
    optionalParam(name : string, _ : NToken, _ : NToken, type : TypescriptType) : TypeScriptParameter
    {
      //System.Diagnostics.Debug.WriteLine("optionalParm");
      TypeScriptParameter(name, type, true, false)
    }
    
    multipleParam(_ : NToken, parm : TypeScriptParameter) : TypeScriptParameter
    {
      TypeScriptParameter(parm.Name, parm.Type, parm.IsOptional, true)
    }
    
    parms(p : TypeScriptParameter, tail : option[(NToken * list[TypeScriptParameter])]) : list[TypeScriptParameter]
    {
      //System.Diagnostics.Debug.WriteLine("parms");
      match(tail)
      {
      | Some((_, tail)) => p :: tail
      | _ => [p]
      }
    }
    
    parmsSemicolon(p : TypeScriptParameter, tail : option[(Nemerle.Peg.NToken * list[TypeScriptParameter])], _ : NToken) : list[TypeScriptParameter]
    {
      match(tail)
      {
      | Some((_, tail)) => p :: tail
      | _ => [p]
      }
    }
    
    methodDeclaration(name : string, isNullable : NToken, _ : NToken, parms : option[list[TypeScriptParameter]], _ : NToken, type : option[(NToken * TypescriptType)],  _ : NToken) : TypeScriptMember.Method
    {
      //System.Diagnostics.Debug.WriteLine("method");
      def type = if(type is Some((_, type))) type else null;
      TypeScriptMember.Method(name, GetText(isNullable) == "?", if(parms is Some(parms)) parms else [], type)
    }
    
    propertyDeclaration(name : string, isNullable : NToken, _ : NToken, type : TypescriptType, _ : NToken) : TypeScriptMember.Property
    {
      //System.Diagnostics.Debug.WriteLine("prop");
      TypeScriptMember.Property(name, GetText(isNullable) == "?", type)
    }
    
    callDeclaration(_ : Nemerle.Peg.NToken, parms : option[list[TypeScriptParameter]], _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, returnType : TypescriptType, _ : Nemerle.Peg.NToken) : TypeScriptMember
    {
      TypeScriptMember.Call(parms, returnType)
    }

    indexCallDeclaration(_ : Nemerle.Peg.NToken, parms : option[list[TypeScriptParameter]], _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, returnType : TypescriptType, _ : Nemerle.Peg.NToken) : TypeScriptMember
    {
      TypeScriptMember.IndexCall(parms, returnType)
    }
    
    memberDeclaration(member : TypeScriptMember) : TypeScriptMember
    {
      //System.Diagnostics.Debug.WriteLine("decl");
      member
    }
    
    interfaceBlock(_ : NToken, declarations : List[TypeScriptMember], _ : NToken) : list[TypeScriptMember]
    {
      //System.Diagnostics.Debug.WriteLine("block");
      declarations.NToList()
    }
    
    extends_(_ : NToken, extendTypes : list[string]) : list[string]
    {
      //System.Diagnostics.Debug.WriteLine("extends");
      extendTypes
    }
    
    _interface(_ : NToken, name : string, extends_ : option[list[string]], members : list[TypeScriptMember]) : TypeScriptGlobalDeclaration
    {
      //System.Diagnostics.Debug.WriteLine("interface: " + name);
      TypeScriptGlobalDeclaration.Interface(name, extends_, members)
    }
    
    globalVarDeclaration(_ : NToken, _ : NToken, name : string, _ : NToken, type : TypescriptType, _ : NToken) : TypeScriptGlobalDeclaration
    {
      TypeScriptGlobalDeclaration.Var(name, type)
    }
    
    globalFunctionDeclaration(_ : NToken, _ : NToken, method : TypeScriptMember) : TypeScriptGlobalDeclaration
    {
      TypeScriptGlobalDeclaration.Function(method :> TypeScriptMember.Method);
    }
    
    start(interfaces : List[TypeScriptGlobalDeclaration]) : list[TypeScriptGlobalDeclaration]
    {
      interfaces.NToList()
    }
  }
  
  public variant TypeScriptGlobalDeclaration
  {
    | Interface { Name : string; Extends : option[list[string]]; Members : list[TypeScriptMember] }
    | Var       { Name : string; Type : TypescriptType }
    | Function  { Method : TypeScriptMember.Method  }
  }
    
  public variant TypeScriptMember : Located
  {
    | Property  { Name : string; IsNullable : bool; Type : TypescriptType }
    | Method    { Name : string; IsNullable : bool; Parms : list[TypeScriptParameter]; Type : TypescriptType }
    | Call      { Parms : option[list[TypeScriptParameter]]; ReturnType : TypescriptType }
    | IndexCall { Parms : option[list[TypeScriptParameter]]; ReturnType : TypescriptType }
  }
  
  [Record]
  public class TypeScriptDeclaration : Located
  {
    public Member : TypeScriptMember;
  }
  
  [Record]
  public class TypeScriptParameter
  {
    public Name : string;
    public Type : TypescriptType;
    public IsOptional : bool;
    public IsParams : bool;
  }
    
  public variant TypescriptType
  {
    | Object { Parms : list[TypeScriptParameter] }
    | Dictionary { Key : TypescriptType; Value : TypescriptType }
    | Array { Type : TypescriptType }
    | Lambda { Parms : option[list[TypeScriptParameter]]; ReturnType : TypescriptType }
    | Normal { Name : string }
  }
}
