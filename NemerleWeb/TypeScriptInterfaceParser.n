using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Peg;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  
  [PegGrammar(Options = EmitDebugSources,
      start,
      grammar {
        newLine                     = "\r\n" / '\n' / '\r' / '\u2028' / '\u2029' / '\u0085';
        singleLineComment           = "//" (!('\n' / '\r') [Any])* newLine?;
        delimitedComment            = "/*" (!"*/" [Any])* "*/";
        comment                     = singleLineComment / delimitedComment;
        spaces : Void               = (' ' / '\t' / '\u000B' / '\u000C' / newLine / comment)*;
        letter                      = ([Lu, Ll, Lt, Lm, Lo, Nl] / '_' / '$');
        decimal_digit               = ['0'..'9'];
        hex_digit                   = ['0'..'9', 'A'..'F', 'a'..'f'];
        octal_digit                 = ['0'..'7'];

        SEMICOLON                   = ';' spaces;
        COLON                       = ':' spaces;
        DOT                         = '.' spaces;
        COMMA                       = ',' spaces;
        LPAREN                      = '(' spaces;
        RPAREN                      = ')' spaces;
        LBRACE                      = '[' spaces;
        RBRACE                      = ']' spaces;
        LFBRACE                     = '{' spaces;
        RFBRACE                     = '}' spaces;
        EQ                          = '=' spaces;
        QUEST                       = '?' spaces;
        
        identifierValue             = letter (letter / decimal_digit)*;
        identifier      : string    = identifierValue spaces;
        identifiers     : list[string] = identifier (COMMA identifiers)?;

        dictionaryType : TypescriptType       = LFBRACE LBRACE "key:" spaces identifier RBRACE COLON identifier SEMICOLON RFBRACE;        
        objectType : TypescriptType           = LFBRACE parmsSemicolon? RFBRACE;        
        arrayType : TypescriptType            = identifier "[]";
        normalType : TypescriptType           = identifier;
        lambdaType : TypescriptType           = LPAREN parms? RPAREN "=>" spaces type;
        type : TypescriptType                 = (arrayType / lambdaType / normalType / objectType / dictionaryType);
        nonOptionalParam : Parameter          = identifier COLON type;
        optionalParam : Parameter             = identifier QUEST COLON type;
        param : Parameter                     = (nonOptionalParam / optionalParam);
        multipleParam : Parameter             = "..." param;
        parms : list[Parameter]               = (param/multipleParam) (COMMA parms)?;
        parmsSemicolon : list[Parameter]      = param (SEMICOLON parmsSemicolon)? SEMICOLON?;
        callDeclaration : Member              = LPAREN parms? RPAREN COLON type SEMICOLON;
        indexCallDeclaration : Member         = LBRACE parms? RBRACE COLON type SEMICOLON;
        methodDeclaration : Member            = identifier QUEST? LPAREN parms? RPAREN (COLON type)? SEMICOLON;
        propertyDeclaration : Member          = identifier QUEST? COLON type SEMICOLON;
        memberDeclaration : Member            = (propertyDeclaration / methodDeclaration / callDeclaration / indexCallDeclaration);
        interfaceBlock : list[Member]         = LFBRACE memberDeclaration* RFBRACE;        
        extends : list[string]                = "extends" spaces identifiers;        
        _interface : Interface                = "interface" spaces identifier extends? interfaceBlock;

        start : list[Interface] = spaces _interface*;
      }
    )]
  public class TypeScriptInterfaceParser
  {
    identifier(idValue : NToken) : string 
    {
      GetText(idValue)
    }

    identifiers(id : string, tail : option[NToken * list[string]]) : list[string] 
    {
      match(tail) {
      | Some((_, tail)) => id :: tail
      | _ => [id]
      }
    }
    
    dictionaryType(_ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, key : string, _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, value : string, _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken) : NemerleWeb.TypescriptType
    {
      TypescriptType.Dictionary(key, value)
    }
    
    objectType(_ : Nemerle.Peg.NToken, parms : option[list[NemerleWeb.Parameter]], _ : Nemerle.Peg.NToken) : NemerleWeb.TypescriptType
    {
      TypescriptType.Object(if(parms is Some(parms)) parms else [])
    }
    
    arrayType(name : string, _ : Nemerle.Peg.NToken) : NemerleWeb.TypescriptType
    {
      TypescriptType.Array(name)
    }

    normalType(name : string) : NemerleWeb.TypescriptType
    {
      TypescriptType.Normal(name)
    }

    lambdaType(_ : Nemerle.Peg.NToken, parms : option[list[NemerleWeb.Parameter]], _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, returnType : NemerleWeb.TypescriptType) : NemerleWeb.TypescriptType    
    {
      TypescriptType.Lambda(parms, returnType)
    }

    nonOptionalParam(name : string, _ : NToken, type : TypescriptType) : Parameter
    {
      Parameter(name, type, false, false)
    }
    
    optionalParam(name : string, _ : NToken, _ : NToken, type : TypescriptType) : Parameter
    {
      Parameter(name, type, true, false)
    }
    
    multipleParam(_ : NToken, parm : Parameter) : Parameter
    {
      Parameter(parm.Name, parm.Type, parm.IsOptional, true)
    }
    
    parms(p : Parameter, tail : option[(NToken * list[Parameter])]) : list[Parameter]
    {
      match(tail)
      {
      | Some((_, tail)) => p :: tail
      | _ => [p]
      }
    }
    
    parmsSemicolon(p : NemerleWeb.Parameter, tail : option[(Nemerle.Peg.NToken * list[NemerleWeb.Parameter])], _ : NToken) : list[NemerleWeb.Parameter]
    {
      match(tail)
      {
      | Some((_, tail)) => p :: tail
      | _ => [p]
      }
    }
    
    methodDeclaration(name : string, isNullable : NToken, _ : NToken, parms : option[list[Parameter]], _ : NToken, type : option[(NToken * TypescriptType)],  _ : NToken) : Member.Method
    {
      def type = if(type is Some((_, type))) type else null;
      Member.Method(name, GetText(isNullable) == "?", if(parms is Some(parms)) parms else [], type)
    }
    
    propertyDeclaration(name : string, isNullable : NToken, _ : NToken, type : TypescriptType, _ : NToken) : Member.Property
    {
      Member.Property(name, GetText(isNullable) == "?", type)
    }
    
    callDeclaration(_ : Nemerle.Peg.NToken, parms : option[list[NemerleWeb.Parameter]], _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, returnType : NemerleWeb.TypescriptType, _ : Nemerle.Peg.NToken) : NemerleWeb.Member
    {
      Member.Call(parms, returnType)
    }

    indexCallDeclaration(_ : Nemerle.Peg.NToken, parms : option[list[NemerleWeb.Parameter]], _ : Nemerle.Peg.NToken, _ : Nemerle.Peg.NToken, returnType : NemerleWeb.TypescriptType, _ : Nemerle.Peg.NToken) : NemerleWeb.Member
    {
      Member.IndexCall(parms, returnType)
    }
    
    memberDeclaration(member : Member) : Member
    {
      member
    }
    
    interfaceBlock(_ : NToken, declarations : List[Member], _ : NToken) : list[Member]
    {
      declarations.NToList()
    }
    
    extends(_ : NToken, extendTypes : list[string]) : list[string]
    {
      extendTypes
    }
    
    _interface(_ : NToken, name : string, extends : option[list[string]], members : list[Member]) : Interface
    {
      Interface(name, extends, members)
    }
    
    start(interfaces : List[Interface]) : list[Interface]
    {
      interfaces.NToList()
    }
  }
  
  [Record]
  public class Interface : Located
  {
    public Name : string;
    public Extends :  option[list[string]];
    public Members : list[Member];
  }
  
  public variant Member : Located
  {
    | Property  { Name : string; IsNullable : bool; Type : TypescriptType }
    | Method    { Name : string; IsNullable : bool; Parms : list[Parameter]; Type : TypescriptType }
    | Call      { Parms : option[list[NemerleWeb.Parameter]]; ReturnType : TypescriptType }
    | IndexCall { Parms : option[list[NemerleWeb.Parameter]]; ReturnType : TypescriptType }
  }
  
  [Record]
  public class Declaration : Located
  {
    public Member : Member;
  }
  
  [Record]
  public class Parameter
  {
    public Name : string;
    public Type : TypescriptType;
    public IsOptional : bool;
    public IsParams : bool;
  }
    
  public variant TypescriptType
  {
    | Object { Parms : list[Parameter] }
    | Dictionary { Key : string; Value : string }
    | Array { Name : string }
    | Lambda { Parms : option[list[NemerleWeb.Parameter]]; ReturnType : TypescriptType }
    | Normal { Name : string }
  }
}
