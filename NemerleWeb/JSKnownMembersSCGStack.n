using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  module JSKnownMembersSCGStack
  {
    public Register() : void
    {
      JSKnownMembers.RegisterType(typeof(System.Collections.Generic.Stack[_]), Handler); 
    }
    
    private Handler(left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      (if (left is JSAst.StaticMethodRef) StaticHandler else InstanceHandler)(left, member, parms)
    }
    
    private InstanceHandler(left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      match (parms)
      {
      | [] =>
        match (member.Name)
        {
        | "get_Count" => Some(JSAst.FieldRef(left, "length"))
        | "Clear" =>
          Some(JSAst.Assign(JSAst.FieldRef(left, "length"), JSAst.Literal(JSLiteral.Number(0))))
        | "GetEnumerator" => Some(left.JSMethodCall(member.Name, parms))
        | "Peek" => 
          // left[left.length - 1]
          Some(
            JSAst.ArrayIndexer(
              left,
              JSAst.Call(
                JSAst.OpCode("-"),
                [
                  left.Field("length"),
                  JSAst.Literal(JSLiteral.Number(1))
                ])
              )
          )
        | "Pop" => Some(left.JSMethodCall(member.Name, parms))
        | "ToArray" => Some(left)
        | _ => None()
        }
        
      | [_] =>
        match (member.Name)
        {
        | "Add" => Some(left.JSMethodCall("push", parms))
        | "Remove"
        | "Push" => Some(left.JSMethodCall(member.Name, parms))
        | _ => None()
        }
      | xs => 
        match (member.Name) 
        {
        | "Add" => Some(left.JSMethodCall("push", [JSAst.Tuple(xs)]))
        | "Remove"
        | "Push" => Some(left.JSMethodCall(member.Name, [JSAst.Tuple(xs)]))
        | _ => None()
        }
      }
    }
     
    private StaticHandler(_left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      match (parms)
      {
      | [] =>
        match (member.Name)
        {
        | ".ctor" => Some(JSAst.Array([]))
        | _ => None()
        }
      | [arg] =>
        match (member.Name)
        {
        | ".ctor" =>
          Some(JSAst.Call(JSAst.GlobalRef("System_Collections_Generic_Stack"), [arg]))
        | _ => None()
        }
      | _ => None()
      }
    }
  }
}
