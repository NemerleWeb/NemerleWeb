using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  module JSKnownMembersEventsDSL
  {
    public Register() : void
    {
      JSKnownMembers.RegisterType(typeof(NemerleWeb.Events), Handler);      
    }
    
    private Handler(_left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      match (parms)
      {
      | [eventName, JSAst.Literal(JSLiteral.String(context)), callback] =>
        match (member.Name)
        {
        | "Handle" => 
          def cb = CreateCallback(callback);
          def context = GetContext(context);
          def code = $<#$$($context).bind($(eventName.Write()), (($(cb.Write())))())#>;
          Some(JSAst.Code(code))
        | "HandleOnce" => 
          def tempVarName = JSAstHelper.GetTemporaryVariableName();
          def context = GetContext(context);
          def cb = CreateCallback(callback);
          Some(JSAst.Code($<#
            var $tempVarName = ($(cb.Write()))();
            $$($context).bind($(eventName.Write()), function(a) {
              $tempVarName(a);
              $$($context).unbind($(eventName.Write()), $tempVarName);
            });
          #>))
        | _ => None()
        }
      | [eventName, JSAst.Literal(JSLiteral.String(context)), untilName, callback] =>
        match(member.Name)
        {
        | "HandleUntil" =>
          def tempVarName = JSAstHelper.GetTemporaryVariableName();
          def eventName = eventName.Write();
          def context = GetContext(context);
          def cb = CreateCallback(callback);
          Some(JSAst.Code($<#
            var $tempVarName = ($(cb.Write()))();
            $$($context).bind($eventName, $tempVarName)
                    .bind($(untilName.Write()), function() {
              $$($context).unbind($eventName, $tempVarName);
            });
          #>))
        | _ => None();
        }
      | [] =>
        match(member.Name)
        {
        | "Invalidate" => Some(JSAst.Code("nweb.invalidate()"))
        | _ => None();
        }
      | _ => Message.FatalError("Unknown Events expression")
      }
    }
    
    private CreateCallback(ast : JSAst) : JSAst
    {
      JSAstHelper.MakeReturningFunction(ast);
    }
    
    private GetContext(context : string) : string
    {
      if(context == "this" || context == "window" || context == "document") $<#$context#> else $<#"$context"#>;
    }
  }
}
