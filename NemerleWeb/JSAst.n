using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Globalization;

namespace NemerleWeb
{
  public module StringLiteral
  {
    // HACK: Indicator to not escape string literal
    // Some string that cannot be found in code
    public static DoNotEscapeMarker = "\u0001";
  }
 
  
    public variant JSAst
    {           
        | Assign
          {
            Left : JSAst;
            Right : JSAst;
            
            public override Write(indent = "" : string) : string
            {              
              indent + $"$(Left.Write()) = $(Right.Write())";
            }
            public override Transform() : JSAst
            {
              match(this) {
              | Assign(_, right) when !IsAllowedForAssignment(right) => 
                def varName = JSAstHelper.GetTemporaryVariableName();                
                JSAst.Seq([
                  JSAst.DefValue(varName, JSAst.Void()),
                  JSAstHelper.DelayAssignment(Right, JSAst.LocalRef(varName, None())),
                  JSAst.Assign(Left, LocalRef(varName, None()))
                ]);
              | _ => this
              }
            }
          }
        | Array          { Values : list[JSAst];
            public override Write(_ = "" : string) : string {
                def Write(el, ind) { el.Write(ind) }
                $<#[..$(Values; ","; Write(_, ""))]#>
            }
            public override Transform() : JSAst
            {
              JSAst.Array(
                Values.SelectMany(
                    x => match(x)
                    {
                    | JSAst.Array(xs2) => xs2
                    | x => [x]
                    })
                  .Where(e => !(e is StaticRef("Nemerle_Core_Nil[T]", _)))
                  .NToList())
            }
            
        }
        | ArrayIndexer    { Obj : JSAst; Index : JSAst;
            public override Write(_ = "" : string) : string {                
                $<#$(Obj.Write())[$(Index.Write())]#>
            }
        }
        | BinaryOperator { Op : string; Left : JSAst; Right : JSAst;
            public override Write(_ = "" : string) : string {
                $"$(Left.Write()) $Op $(Right.Write())"
            }
        }
        | Break          {
            public override Write(indent = "" : string) : string {
                indent + "break;"
            }
        }
        | BreakNamed     { Id : string;
            public override Write(indent = "" : string) : string {
                indent + $"break $Id;"
            }
        }
        | Call
          {
            Fun : JSAst;
            Parameters : list[JSAst];
            
            public override Write(indent = "" : string) : string
            {
              def writeParam(p) { p.Write("") }
              def writeParamsRaw(parms) { $<#..$(parms; ", "; writeParam)#> }
              def writeParams(parms) { $"($(writeParamsRaw(parms)))" }
              
              def output =
                match(this)
                {
                | Call(OpCode as op, [e1, e2])         =>
                  $<#($(e1.Write("")) $(op.Write("")) $(e2.Write("")))#>
                
                | Call(Constructor(type, None), parms) => $<#new $(type)$(writeParams(parms))#>
                | Call(Constructor(type, Some(paramsType)), parms) => 
                  def methodSignature = JSAstHelper.MethodSignature(paramsType);
                  def comma = if (parms is []) "" else ", ";
                  $<#new $(type)($methodSignature$comma$(writeParamsRaw(parms)))#>
                
                | Call(ConstructorRef(type, None), parms) =>
                  $<#$(type).call$(writeParams(JSAst.This() :: parms))#>
                
                | Call(ConstructorRef(type, Some(paramsType)), parms) =>
                  def methodSignature = JSAstHelper.MethodSignature(paramsType);
                  def comma = if (parms is []) "" else ", ";
                  def astThis = JSAst.This().Write();
                  $<#$(type).call($astThis, $methodSignature$comma$(writeParamsRaw(parms)))#>
                
                | Call(Indexer(obj), parms)          =>
                  $<#$(obj.Write())[$(writeParamsRaw(parms))]#>                
                
                | Call(MethodRef(obj, method, Some) as f, parms) when
                  !JSAstHelper.IsSpecialMethod(method) =>
                  $<#$(JSApiSpecial.JSUnescapeName(f.Write())).call$(writeParams(obj :: parms))#>
                
                | Call(Function as f, parms) => $<#($(f.Write()))$(writeParams(parms))#>
                | Call(f, parms)             => $<#$(f.Write())$(writeParams(parms))#>    
                };
              
              indent + output
            }
            
            static private Literal_Int1 : JSAst.Literal = JSAst.Literal(JSLiteral.Integer(1));
            
            public override Transform() : JSAst
            {
              // Compiler bug workaround
              def parametersNotAllowedForAssignment = Parameters.Any(p => !IsAllowedForAssignment(p));
              match(this)
              {
              | Call(OpCode("==="), something :: Literal(Bool(true)) :: []) 
              | Call(OpCode("==="), Literal(Bool(true)) :: something :: []) => something.Transform()
              | Call(OpCode as op, Call(MethodRef(Seq,  _, _), _) as call :: rest) => 
                  def varName = JSAstHelper.GetTemporaryVariableName();
                  JSAst.Seq(
                    [
                      JSAst.DefValue(varName, JSAst.Void()),
                      JSAstHelper.DelayAssignment(call, JSAst.LocalRef(varName, None())),
                      JSAst.Call(op, JSAst.LocalRef(varName, None()) :: rest)
                    ]
                  ).Transform()
              | Call(OpCode as op, head :: (Call(MethodRef(Seq,  _, _), _) as call) :: []) => 
                  def varName = JSAstHelper.GetTemporaryVariableName();
                  JSAst.Seq(
                    [
                      JSAst.DefValue(varName, JSAst.Void()),
                      JSAstHelper.DelayAssignment(call, JSAst.LocalRef(varName, None())),
                      JSAst.Call(op, [head, JSAst.LocalRef(varName, None())])
                    ]
                  ).Transform()
              | Call(OpCode("==="), something :: Literal(Null) :: []) 
              | Call(OpCode("==="), [Literal(Null), something]) => 
                  JSAst.Call(
                    JSAst.OpCode("||"), 
                    [
                      JSAst.Call(JSAst.OpCode("==="), [something, JSAst.Literal(JSLiteral.Null())]), 
                      JSAst.Call(
                        JSAst.OpCode("==="),
                        [
                          JSAst.TypeOf(something),
                          JSAst.Literal(JSLiteral.String("undefined"))
                        ])
                    ]
                  ).Transform()
                 
              // Empty list comparison
              | Call(OpCode("===") as op, [something, JSAst.Array([])])
              | Call(OpCode("===") as op, [JSAst.Array([]), something])
              | Call(OpCode("!==") as op, [something, JSAst.Array([])])
              | Call(OpCode("!==") as op, [JSAst.Array([]), something]) => 
                JSAst.Call(op, [something.Field("length"), JSAst.Literal(JSLiteral.Integer(0))])
              
              | Call(OpCode("!=="), something :: Literal(Null) :: []) 
              | Call(OpCode("!=="), [Literal(Null), something]) => 
                  JSAst.Call(
                    JSAst.OpCode("&&"), 
                    [
                      JSAst.Call(JSAst.OpCode("!=="), [something, JSAst.Literal(JSLiteral.Null())]), 
                      JSAst.Call(
                        JSAst.OpCode("!=="),
                        [
                          JSAst.TypeOf(something),
                          JSAst.Literal(JSLiteral.String("undefined"))
                        ])
                    ]
                  ).Transform()                 
                
              | Call(func, parms) when parms.OfType.[JSAst.Seq]().Any() =>
                  def newParms = parms.Select(p : JSAst => {
                      def varName = JSAstHelper.GetTemporaryVariableName();
                      match(p)
                      {
                      | xs is Seq =>
                        (JSAst.Seq(
                          [
                            JSAst.DefValue(varName, JSAst.Void()),
                            JSAstHelper.DelayAssignment(xs, JSAst.LocalRef(varName, None()))
                          ]),
                        JSAst.LocalRef(varName, None()))
                      | x => (JSAst.Void(), x)
                      }
                  }).NToList();
                  def paramInits = JSAst.Seq(newParms.Filter((i, _)=> !(i is JSAst.Void)).Map((i, _) => i));
                  def newParms = newParms.Map((_, p) => p);
                  JSAst.Seq([paramInits, JSAst.Call(func, newParms).Transform()])
              | Call(OpCode("++" as opcode), [FieldRef as field])
              | Call(OpCode("--" as opcode), [FieldRef as field]) =>
                Call(OpCode(opcode.Substring(1)), [field, Literal_Int1])
                
              | Call(OpCode("++" as opcode), [PropertyGet as property])
              | Call(OpCode("++" as opcode), [Call(MethodRef, _) as property])
              | Call(OpCode("--" as opcode), [PropertyGet as property ])
              | Call(OpCode("--" as opcode), [Call(MethodRef, _) as property]) =>
                Call(OpCode(opcode.Substring(1)), [property, Literal_Int1])
              /* TODO: _ when (() => false)() crashes compiler */              
              | Call(func, parms) when parametersNotAllowedForAssignment =>
                def varInitializations = 
                  parms.Map(p =>
                    if(!IsAllowedForAssignment(p))
                    {
                      def varName = JSAstHelper.GetTemporaryVariableName();
                      (Some(varName), [JSAst.DefValue(varName, JSAst.Void()), JSAstHelper.DelayAssignment(p, JSAst.LocalRef(varName, None()))])
                    }
                    else
                      (None(), [p]));
                      
                def varParams = varInitializations.Map(
                  v =>
                    match(v) 
                    { 
                    | (Some(name), _) => JSAst.LocalRef(name, None()) 
                    | (None, [p]) => p 
                    | _ => Message.FatalError("Shouldn't happen")
                    });
                
                JSAst.Seq(varInitializations
                  .SelectMany(
                    v => 
                      match(v) 
                      { 
                      | (Some, seq) => seq 
                      | _ => [] 
                      })
                 .Concat([JSAst.Call(func, varParams).Transform()])
                 .NToList())
              | Call(MethodRef(obj, meth, p), parms) when !IsAllowedForAssignment(obj) =>
                def varName = JSAstHelper.GetTemporaryVariableName(); 
                JSAst.Seq([
                  JSAst.DefValue(varName, JSAst.Void()),
                  JSAstHelper.DelayAssignment(obj, JSAst.LocalRef(varName, None())),
                  JSAst.Call(JSAst.MethodRef(JSAst.LocalRef(varName, None()), meth, p), parms).Transform()
                ])
              // TODO: Improve this hack for -= , *= and other
              | Call(OpCode("+="), [Call(OpCode("+="), [l1, r1]), r2]) =>                                
                  JSAst.Call(OpCode("+="), [l1, JSAst.Call(JSAst.OpCode("+"), [r1, r2])])
              | _ => this
              }
            }
          }
        | Code           { CodeString : string;
            public override Write(indent = "" : string) : string {
                indent + $"$CodeString"
            }
        }
        | Continue       { LoopName : string;
            public override Write(indent = "" : string) : string {
                indent + $"continue $LoopName"
            }
        }
        | Constructor   
          {
            Type : string;
            ParametersType : option[list[TParameter]];
          
            public override Write(_ = "" : string) : string
            {
              throw NotImplementedException("Not implemented");
            }
          }
        | ConstructorRef
          {
            Type : string;
            ParametersType : option[list[TParameter]];
          
            public override Write(_ = "" : string) : string
            {
              Type
            }
          }
        | DefValue       { Name : string; Val : JSAst;
          public override Write(indent = "" : string) : string {            
            match(this)
            {
            | DefValue(name, Void) => indent + $<#var $(GetJSName(name))#>
            | DefValue(name, val)  => indent + $<#var $(GetJSName(name)) = $(val.Write())#>
            }
          }
          public override Transform() : JSAst
          {
            match(this)
            {
            | DefValue(l, r) when !IsAllowedForAssignment(r) =>
              def res = JSAst.Seq([
                JSAst.DefValue(l, JSAst.Void()),
                JSAstHelper.DelayAssignment(r, JSAst.LocalRef(l, None()))]);
              res;
            | _ => this                
            }
          }            
        }
        | Delete
          {
            Obj : JSAst;
          
            public override Write(indent = "" : string) : string
            {
              $"$(indent)delete $(Obj.Write())"
            }
          }
        | For            { ItName : string; Collection : JSAst; Body : JSAst;
            public override Write(_indent = "" : string) : string {
$<#
for(var $ItName in $(Collection.Write())) {
var $ItName = $(Collection.Write())[$ItName];
$(Body.Write("  "))
}#> 
            }
        }
        | Function
          {
            Name : string;
            Parameters : list[JSAst];
            Body : JSAst;
          
            public override Write(indent = "" : string) : string
            {
$<#$(indent)function $Name(..$(Parameters; ";"; p => p.Write(""))) {
$(Body.Write(indent + "  "))
$(indent)}#>
            }
          }
        | GlobalRef
          {
            Member : string;
          
            public override Write(_ = "" : string) : string
            {
              GetDefinedJSName(Member)
            }
          }
        | Goto           { LabelId : string;
            public override Write(_ = "" : string) : string {
                "/* No goto writer implementation */"
            }
        }
        | HasType        { Obj : JSAst; TypeName : string;
          public override Write(_indent = "" : string) : string {
            $"$(Obj.Write()).constructor === $TypeName"
          }
          public override Transform() : JSAst {
            match(this) {
            | HasType(l, "Nemerle_Core_Cons_$T$__$T$_")
            | HasType(l, "Nemerle_Core_Cons_$T$_") => 
              JSAst.Call(JSAst.OpCode(">"),
                        [JSAst.FieldRef(l, "length"), JSAst.Literal(JSLiteral.Integer(0))]);
            | _ => this                
            }
          }   
        }
        | If             { Cond : JSAst; Body : JSAst; ElseBody : JSAst;
            public override Write(indent = "" : string) : string {
$<#
$(indent)if($(Cond.Write())) { 
$(Body.Write(indent + "  "))
$indent} else {
$(ElseBody.Write(indent + "  "))
$indent}#>
            }
            public override Transform() : JSAst {
                match(this) {
                | If(If(c, t, e), tt, ee)    => 
                  def varName = JSAstHelper.GetTemporaryVariableName();
                  JSAst.Seq([
                    JSAst.DefValue(varName, JSAst.If(c, t, e)).Transform(),
                    JSAst.If(JSAst.LocalRef(varName, None()), tt, ee)
                  ]).Transform();
                | If(Call(func, [something, If(c, t, e)]), then, els) => 
                  def varName = JSAstHelper.GetTemporaryVariableName();
                  JSAst.Seq([
                    JSAst.DefValue(varName, JSAst.If(c, t, e)).Transform(),
                    JSAst.If(JSAst.Call(func, [something, JSAst.LocalRef(varName, None())]), then, els)
                  ]).Transform();
                | If(Call(func, [If(c, t, e), something]), then, els) => 
                  def varName = JSAstHelper.GetTemporaryVariableName();
                  JSAst.Seq([
                    JSAst.DefValue(varName, JSAst.If(c, t, e)).Transform(),
                    JSAst.If(JSAst.Call(func, [JSAst.LocalRef(varName, None()), something]), then, els)
                  ]).Transform();
                | If(Call(OpCode as op, [Seq as seq, rest]), then, els) =>
                    def varName = JSAstHelper.GetTemporaryVariableName();
                    JSAst.Seq([JSAst.DefValue(varName, JSAst.Void()), JSAstHelper.DelayAssignment(seq, JSAst.LocalRef(varName, None())), JSAst.If(JSAst.Call(op, [JSAst.LocalRef(varName, None()), rest]), then, els)]).Transform();                           
                | If(Call(OpCode as op, [Call(MethodRef(Seq as seq, m, p), parms), rest]), then, els)  =>             
                    def varName = JSAstHelper.GetTemporaryVariableName();
                    JSAst.Seq(
                      [
                        JSAst.DefValue(varName, JSAst.Void()),
                        JSAstHelper.DelayAssignment(seq, JSAst.LocalRef(varName, None())),
                        JSAst.If(
                          JSAst.Call(op, 
                            [
                              JSAst.Call(
                                JSAst.MethodRef(JSAst.LocalRef(varName, None()), m, p),
                                parms),
                              rest
                            ]),
                          then,
                          els)])
                      .Transform();
                | If(Call(OpCode as op, [Call as call, rest]), then, els) =>             
                    def varName = JSAstHelper.GetTemporaryVariableName();
                    JSAst.Seq([JSAst.DefValue(varName, call), JSAst.If(JSAst.Call(op, [JSAst.LocalRef(varName, None()), rest]), then, els)]).Transform()
                | If(Seq as seq, then, els) => 
                    def varName = JSAstHelper.GetTemporaryVariableName();
                    JSAst.Seq([JSAst.DefValue(varName, JSAst.Void()), 
                               JSAstHelper.DelayAssignment(seq, JSAst.LocalRef(varName, None())), 
                               JSAst.If(JSAst.Call(JSAst.OpCode("==="), 
                                                   [JSAst.LocalRef(varName, None()), JSAst.Literal(JSLiteral.Bool(true))]), 
                                        then, els)])
                          .Transform();   
                | _ => this
                }
            }
        }
        | Indexer        { Obj : JSAst;
            public override Write(_ = "" : string) : string {
                "/* No Indexer writer implementation */"
            }
        }      
        | JustIf         { Cond : JSAst; Body : JSAst;
            public override Write(indent = "" : string) : string {
$<# 
$(indent)if($(Cond.Write())) { 
$(Body.Write(indent + "  "))
$(indent)}#>
            }
            public override Transform() : JSAst {
                match(this) {
                | JustIf(Call(func, [something, If(c, t, e)]), then) => 
                  def varName = JSAstHelper.GetTemporaryVariableName();
                  JSAst.Seq([
                    JSAst.DefValue(varName, JSAst.If(c, t, e)).Transform(),
                    JSAst.JustIf(JSAst.Call(func, [something, JSAst.LocalRef(varName, None())]), then)
                  ]).Transform();
                | JustIf(Call(func, [If(c, t, e), something]), then) => 
                  def varName = JSAstHelper.GetTemporaryVariableName();
                  JSAst.Seq([
                    JSAst.DefValue(varName, JSAst.If(c, t, e)).Transform(),
                    JSAst.JustIf(JSAst.Call(func, [JSAst.LocalRef(varName, None()), something]), then)
                  ]).Transform();
                | JustIf(Call(OpCode as op, Seq as seq :: rest), then) =>
                    def varName = JSAstHelper.GetTemporaryVariableName();
                    JSAst.Seq([JSAst.DefValue(varName, JSAst.Void()), JSAstHelper.DelayAssignment(seq, JSAst.LocalRef(varName, None())), JSAst.JustIf(JSAst.Call(op, JSAst.LocalRef(varName, None()) :: rest), then)]).Transform();                           
                | JustIf(Call(OpCode as op, Call(MethodRef(Seq as seq,  m, p), parms) :: rest), then)  =>             
                    def varName = JSAstHelper.GetTemporaryVariableName();
                    JSAst.Seq(
                      [
                        JSAst.DefValue(varName, JSAst.Void()),
                        JSAstHelper.DelayAssignment(seq, JSAst.LocalRef(varName, None())),
                        JSAst.JustIf(
                          JSAst.Call(
                            op,
                            JSAst.Call(
                              JSAst.MethodRef(JSAst.LocalRef(varName, None()), m, p),
                              parms) :: rest),
                          then)
                      ]
                    ).Transform();
                | JustIf(Call(OpCode as op, Call as call :: rest), then) =>             
                    def varName = JSAstHelper.GetTemporaryVariableName();
                    JSAst.Seq([JSAst.DefValue(varName, call), JSAst.JustIf(JSAst.Call(op, JSAst.LocalRef(varName, None()) :: rest), then)]).Transform()
                | JustIf(Seq as seq, then) => 
                    def varName = JSAstHelper.GetTemporaryVariableName();
                    JSAst.Seq([JSAst.DefValue(varName, JSAst.Void()), 
                               JSAstHelper.DelayAssignment(seq, JSAst.LocalRef(varName, None())), 
                               JSAst.JustIf(JSAst.Call(JSAst.OpCode("==="), 
                                                   [JSAst.LocalRef(varName, None()), JSAst.Literal(JSLiteral.Bool(true))]), then)])
                          .Transform();   
                | _ => this
                }
            }
        }               
        | Label          { Id : int; Body : JSAst;
          public override Write(indent = "" : string) : string {
            Body.Write(indent)
          }
        }
        | Lambda         { Name : string; Parameters : list[string]; Body : JSAst;
            public override Write(indent = "" : string) : string {
$<#$(indent)function(..$Parameters) {
$(Body.Write(indent + "  "))
}#>
            }
        }
        | Literal        
          {
            Value : JSLiteral;
            
            public override Write(indent = "" : string) : string
            {
              match(this)
              {
              | Literal(Null)         => indent + "null"
              | Literal(Integer(val)) => indent + $"$(val.ToString(CultureInfo.InvariantCulture))"
              | Literal(Float(val))   => indent + $"$(val.ToString(CultureInfo.InvariantCulture))"
              | Literal(String(val))  =>        
                def escaping = val.IndexOf(StringLiteral.DoNotEscapeMarker);
                def str = 
                  if(escaping != -1)
                    val.Substring(0, escaping)
                  else
                    JSAstHelper.EscapeString(val);
                    
                indent + str                
              | Literal(Bool(val))    => indent + $"$(val.ToString(CultureInfo.InvariantCulture).ToLower())"
              | Literal(Regexp(pat, mod)) => indent + $<#new RegExp($(pat.Write()), $(mod.Write()))#>
              }
            }
        }
        | LocalRef       
          {
            Name : string;
            Type : option[TypeVar];
            
            public override Write(_ = "" : string) : string
            {
              GetDefinedJSName(Name)
            }
          }
        | ParamRef       { Name : string; Type : option[TypeVar];
            public override Write(_ = "" : string) : string {
                $<#$(GetDefinedJSName(Name)).value#>
            }
        }
        | MethodRef
          { 
            Obj : JSAst; 
            Method : string;
            ParametersType : option[list[TParameter]];
            
            public override Write(_ = "" : string) : string
            {
              def delimiter = if (Obj is Literal(JSLiteral.Integer)) " " else "";
              // HACK: Lambda support
              if (Method == "apply" || Method == "apply_void")
                $"$(Obj.Write())"
              else
              {
                // Handle empty method name
                def call = if(Method != "") "." else "";

                $"$(Obj.Write())$delimiter$call$(JSAstHelper.Method(Method, ParametersType))"
              }
            }
          }    
        | StaticMethodRef
          {
            Type : string;
            Method : string;
            ParametersType : option[list[TParameter]];
            
            public override Write(_ = "" : string) : string
            {
              def type = if (Type.IsEmpty()) "" else $"$Type.";
              $"$type$(JSAstHelper.Method(Method, ParametersType))"
            }
        }
        | StaticRef
          { 
            Type : string;
            Member : string;            
            
            public override Write(_ = "" : string) : string 
            {
              def type = if (Type.IsEmpty()) "" else $"$Type.";
              $"$type$(GetDefinedJSName(Member))"
            }
          }
        | FieldRef       { Obj : JSAst; Parameter : string;
            public override Write(_ = "" : string) : string {
                $"$(Obj.Write()).$Parameter"
            }
        }
        | PropertySet    
          {
            Obj : JSAst;
            PropertyName : string;
            Value : JSAst;
            
            public override Write(indent = "" : string) : string 
            {
              indent + $"$(Obj.Write()).set_$PropertyName($(Value.Write()))"
            }
            
            public override Transform() : JSAst 
            {
              JSAst.Call(JSAst.MethodRef(Obj, "set_" + PropertyName, None()), [Value]).Transform()
            }
          }
        | PropertyGet   
          {
            Obj : JSAst;
            PropertyName : string;
            
            public override Write(_ = "" : string) : string 
            {
              $"$(Obj.Write()).get_$PropertyName()"
            }
          }
        | OpCode         { Name : string;
            public override Write(_ = "" : string) : string {
                $"$Name"
            }
        }
        | Object         { Fields : list[(string * JSAst)];
            public override Write(_ = "" : string) : string {
                def fieldToObjectField((name, ast)) {
                    $"$name : $(ast.Write())"
                }
                $<#{ ..$(Fields; ", "; fieldToObjectField) }#>
            }
        }
        | Return         { Expr : JSAst;
            public override Write(indent = "" : string) : string {
                indent + $<#return $(Expr.Write(""))#>
            }
        }
        | Seq            { Items : list[JSAst];
            public override Write(indent = "" : string) : string {                
                def ss = Items.Where(i => !(i is JSAst.Void))
                              .Select(i => i.Write(indent));                
                $<#..$(ss; ";" + Environment.NewLine)#>
            }
            public override Transform() : JSAst {
                match(Items) {
                | x :: [] => x
                //| xs => JSAst.Seq(xs.Map(x => x.Transform()))
                | _ => this
                }
            }
        }
        | Switch         { Expr : JSAst; Cases : list[JSAst.SwitchCase]; DefaultExpr : JSAst;
            public override Write(indent = "" : string) : string {
$<#
$(indent)switch($(Expr.Write())) {
..$(Cases; "\n"; e => e.Write(indent + "  "))
$(indent)default:
$(DefaultExpr.Write(indent + "  "))
$(indent)}#>
            }
        }
        | SwitchCase     { Id : JSAst.Literal; Body : JSAst;
            public override Write(indent = "" : string) : string {
$<#$(indent)case $(Id.Write()):
$(Body.Write(indent + "  "))
$(indent)break;#>
            }
        }
        | ServerCall     { MethodName : string; TypeName : string; Parameters : list[string * JSAst]; Callback : JSAst;
            public override Write(indent = "" : string) : string {                            
              def jsparms = Parameters.Map((name, val) => $"$name: ' + JSON.stringify(nweb.utils.normalizeObjectForServer($(val.Write()))) + '");
              def callback = Callback.Write().TrimEnd(';');
$<#$$.ajax({
$(indent)  url: '/$TypeName/$MethodName',
$(indent)  type: 'POST',
$(indent)  dataType: 'json',
$(indent)  data: { model: '{..$jsparms}' },
$(indent)  success: function(result) {
$(indent)    var typed = nweb.utils.toTypedObject(result);
$(indent)    if(result != null && nweb.utils.isTuple(result))
$(indent)      $(callback).apply(undefined, typed);
$(indent)    else
$(indent)      $(callback)(typed);
$(indent)
$(indent)    nweb.invalidate();
$(indent)  }
$(indent)})#>
            }
        }    
        | Ternary        { Cond : JSAst; Body : JSAst; ElseBody : JSAst;
            public override Write(_ : string) : string {
                $"($(Cond.Write()) ? $(Body.Write()) : $(ElseBody.Write()))"
            }
        }
        | This           {
            public override Write(_ = "" : string) : string {
                "self"
            }
        }
        | Throw          { Obj : JSAst;
            public override Write(indent = "" : string) : string {
                indent + $"throw $(Obj.Write())"
            }
        }
        | Try            { Body : JSAst; Handler : JSAst;
            public override Write(indent = "" : string) : string {
$<#
$(indent)try {
$(Body.Write(indent + "  "))
$(indent)} catch {
$(Handler.Write(indent + "  "))
$(indent)}
#>
            }
        }
        | TryFinally     { Body : JSAst; Handler : JSAst;
            public override Write(indent = "" : string) : string {
$<#
$(indent)try {
$(Body.Write(indent + "  "))
$(indent)} finally {
$(Handler.Write(indent + "  "))
$(indent)}
#>
            }
        }
        | Tuple          { Args : list[JSAst];
          public override Write(_indent = "" : string) : string {
            def values = Args.MapI((i, a) => $"$i: $(a.Write())");
            $<#{..$values}#>
          }
        }
        | TupleIndexer          { Obj : JSAst; Pos : int;
          public override Write(_indent = "" : string) : string {            
            $<#$(Obj.Write())[$Pos]#>
          }
        }
        | TypeOf         { Expr : JSAst;
            public override Write(_ = "" : string) : string {
                $<#typeof $(Expr.Write())#>
            }
        }
        | Undefined      {
            public override Write(_ = "" : string) : string {
                <#"undefined"#>
            }
        }
        | UnaryOperator  { Op : string; Obj : JSAst;
            public override Write(_ = "" : string) : string {                
                $"$Op$(Obj.Write())"
            }
        }
        | Void           {
            public override Write(_ = "" : string) : string {
                ""
            }
        }
            
        | While          { Cond : JSAst; Body : JSAst;
            public override Write(indent = "" : string) : string {
$<#
$(indent)while($(Cond.Write())) {
$(Body.Write(indent + "  "))
$(indent)}
#>
            }
        }
        | WhileNamed     { Cond : JSAst; Body : JSAst; Name : string;
            public override Write(indent = "" : string) : string {
$<#
$(indent)$Name: while($(Cond.Write())) {
$(Body.Write(indent + "  "))
$(indent)}
#>  
            }
        }
        | Pack     { Expr : JSAst; 
          public override Write(indent = "" : string) : string {
            JSAst.Assign(Expr, JSAst.Object([("value", Expr)])).Write(indent)
          }
        }
        | Unpack     { Expr : JSAst; 
          public override Write(indent = "" : string) : string {
            JSAst.Assign(Expr, JSAst.FieldRef(Expr, "value")).Write(indent)
          }
        }
        | NotImpl { 
            Val : string;
            public this() {
                Val = "";
            }
            public override Write(_ = "" : string) : string {
                $<#"Not implemented: $Val "#>
            }
        }
        
        | Error
          {
            public override Write(_ = "" : string) : string
            {
              <# Error in the code #>
            }
          }
        
        public abstract Write(_indent = "" : string) : string { string.Empty; };
        public virtual Transform() : JSAst { this }        
        public static AllowedForAssignment : HashSet[Type] =
          HashSet([
            typeof(JSAst.Call),
            typeof(JSAst.MethodRef),
            typeof(JSAst.LocalRef),            
            typeof(JSAst.ParamRef),
            typeof(JSAst.Literal), 
            typeof(JSAst.Code),
            typeof(JSAst.Void), 
            typeof(JSAst.Ternary),
            typeof(JSAst.NotImpl),
            typeof(JSAst.Lambda),
            typeof(JSAst.Array),
            typeof(JSAst.StaticRef), 
            typeof(JSAst.This), 
            typeof(JSAst.Tuple),
            typeof(JSAst.TupleIndexer), 
            typeof(JSAst.FieldRef),
            typeof(JSAst.PropertyGet),
            typeof(JSAst.ArrayIndexer),
            typeof(JSAst.Label),
            typeof(JSAst.Object),
            typeof(JSAst.Unpack),
            typeof(JSAst.GlobalRef),
            typeof(JSAst.ServerCall),
          ]);
        
        public static Recurse(ast : JSAst, selector : (JSAst * int) -> JSAst, currentDepth = 0 : int, topdown : bool = true) : JSAst 
        {
            def recurse = Recurse(_, selector, currentDepth + 1, topdown);
            def ast' = if(topdown) selector(ast, currentDepth) else ast;
            
            def result = match(ast') {
            | Assign(l, r)                => JSAst.Assign(recurse(l), recurse(r))
            | Array(v)                    => JSAst.Array(v.Map(recurse))
            | ArrayIndexer(o, v)          => JSAst.ArrayIndexer(recurse(o), recurse(v))
            | BinaryOperator(o, l, r)     => JSAst.BinaryOperator(o, recurse(l), recurse(r))
            | Break                       => ast'
            | BreakNamed                  => ast'
            | Call(func, parms)           => JSAst.Call(recurse(func), parms.Map(recurse))
            | Code                        => ast'
            | Constructor                 => ast'
            | ConstructorRef              => ast'
            | Continue(loopname)          => JSAst.Continue(loopname)            
            | DefValue(name, val)         => JSAst.DefValue(name, recurse(val))
            | Delete                      => ast'
            | Error                       => ast'
            | FieldRef(obj, parm)         => JSAst.FieldRef(recurse(obj), parm)
            | For(it, coll, body)         => JSAst.For(it, recurse(coll), recurse(body))
            | Function(name, ps, bdy)     => JSAst.Function(name, ps.Map(recurse), recurse(bdy))            
            | GlobalRef                   => ast'
            | Goto                        => ast'
            | HasType(obj, type)          => JSAst.HasType(recurse(obj), type)
            | If(cond, then, els)         => JSAst.If(recurse(cond), recurse(then), recurse(els))
            | Indexer(obj)                => JSAst.Indexer(recurse(obj))
            | JustIf(cond, then)          => JSAst.JustIf(recurse(cond), recurse(then))
            | Label(id, body)             => JSAst.Label(id, recurse(body))
            | Lambda(name, parms, body)   => JSAst.Lambda(name, parms, recurse(body))
            | Literal                     => ast'
            | LocalRef                    => ast'
            | MethodRef(obj, meth, parms) => JSAst.MethodRef(recurse(obj), meth, parms)
            | NotImpl                     => ast'
            | OpCode                      => ast'
            | Object(fields)              => JSAst.Object(fields.Map((name, body) => (name, recurse(body))))
            | ParamRef                    => ast'
            | PropertySet(obj, prop, val) => JSAst.PropertySet(recurse(obj), prop, recurse(val))
            | PropertyGet(obj, prop)      => JSAst.PropertyGet(recurse(obj), prop)
            | Return(expr)                => JSAst.Return(recurse(expr))
            | Seq(items)                  => JSAst.Seq(items.Map(ast => Recurse(ast, selector, currentDepth, topdown)))
            | ServerCall(meth, ty, parms, callback) => JSAst.ServerCall(meth, ty, parms.Map(p => (p[0], recurse(p[1]))), recurse(callback))
            | StaticRef as s              => s
            | StaticMethodRef as s        => s
            | Switch(expr, cases, deflt)  => JSAst.Switch(recurse(expr), cases.Select(recurse).Cast.[JSAst.SwitchCase]().NToList(), recurse(deflt))
            | SwitchCase(id, case)        => JSAst.SwitchCase(id, recurse(case))
            | Ternary(cond, then, els)    => JSAst.Ternary(recurse(cond), recurse(then), recurse(els))
            | This                        => ast'
            | Throw(obj)                  => JSAst.Throw(recurse(obj))
            | Try(body, handler)          => JSAst.Try(recurse(body), recurse(handler))
            | TryFinally(body, handler)   => JSAst.TryFinally(recurse(body), recurse(handler))
            | Tuple(args)                 => JSAst.Tuple(args.Map(recurse))
            | TupleIndexer(o, pos)        => JSAst.TupleIndexer(recurse(o), pos)
            | TypeOf(expr)                => JSAst.TypeOf(recurse(expr))
            | Undefined                   => ast'            
            | UnaryOperator(op, obj)      => JSAst.UnaryOperator(op, recurse(obj))
            | Void                        => ast'        
            | While(cond, body)           => JSAst.While(recurse(cond), recurse(body))
            | WhileNamed(cnd, bdy, nme)   => JSAst.WhileNamed(recurse(cnd), recurse(bdy), nme)
            | Pack                        => ast'
            | Unpack                      => ast'
            }
            
            if(topdown) result else selector(result, currentDepth)
        }
        
        private IsAllowedForAssignment(ast : JSAst) : bool
        {
          AllowedForAssignment.Contains(ast.GetType())
        }
        
        private GetJSName(name : string) : string
        {
          name.Replace('.', '_')
        }
        
        private GetDefinedJSName(name : string) : string
        {
          if(name == JSApiSpecial.JSThis) "this" else GetJSName(name)
        }
    }
}
