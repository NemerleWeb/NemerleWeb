using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace NemerleWeb
{
  /// <summary>
  /// Description of JSKnownMembersInt.
  /// </summary>
  module JSKnownMembersIntegers
  {
    public Register() : void
    {
      JSKnownMembers.RegisterType(typeof(byte), Handler);
      JSKnownMembers.RegisterType(typeof(sbyte), Handler);
      JSKnownMembers.RegisterType(typeof(short), Handler);
      JSKnownMembers.RegisterType(typeof(ushort), Handler);
      JSKnownMembers.RegisterType(typeof(int), Handler);
      JSKnownMembers.RegisterType(typeof(uint), Handler);
      JSKnownMembers.RegisterType(typeof(long), Handler);
      JSKnownMembers.RegisterType(typeof(ulong), Handler);
    }
    
    private Handler(left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      if (left is JSAst.StaticMethodRef) StaticHandler(left, member, parms) else None()
    }
    
    private StaticHandler(_left : JSAst, member : IMember, parms : list[JSAst]) : option[JSAst]
    {
      match (parms)
      {
      | [_] =>
        match (member.Name)
        {
        | "Parse" => Some(JSAst.Call(JSAst.GlobalRef("parseInt"), parms))
        | _ => None()
        }
      | [arg1, _] =>
        match (member.Name)
        {
        | "Parse" => Some(JSAst.Call(JSAst.GlobalRef("parseInt"), [arg1]))
        | "TryParse" =>
          Some(
            JSAst.Call(
              JSAst.MethodRef(
                JSAst.FieldRef(JSAst.GlobalRef("nweb"), "utils"),
                "tryParseInt",
                None()),
              parms))
        | _ => None()
        }
      | _ => None()
      }
    }
  }
}
