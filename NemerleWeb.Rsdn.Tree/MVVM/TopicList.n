using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Extensions;

using System;
using System.Collections.Generic;
using System.Linq;
using NemerleWeb;
using NemerleWeb.TypedJS;

using Rsdn.Tree.Models;

namespace Rsdn
{    
  [Unit]
  public class TopicList
  {
    [Dto] public class Topic 
    { 
      Name : string; 
      Author : string; 
      Rating : int; 
      Comments : int; 
      Id : int;
      LastAnswerBy : string;
      LastUpdatedOn : string;
      CreatedOn : string;
      Agrees : int;
      Disagrees : int;
      Smiles : int;
      public LolIconCount : int { get { Smiles / 3; } }
      public SmileIconCount : int { get { Smiles % 3; } }
    }

    private mutable _url : string;
    private mutable _topics : List[Topic];
    private mutable _selectedTopic : CommentNode;
    private mutable _maxDepth : int;
    private mutable _isLoading : bool;

    public mutable Name : string;
    public Url : string 
    { 
      get { _url; } 
      set { 
        _url = value;
        _selectedTopic = null;
        _ = server.GetTopicList(value, topics => _topics = topics.ToList());
      }
    }
    
    OpenTopic(topic : Topic) : void
    {
      _isLoading = true;
      _ = server.LoadReplies(topic.Id, (node) => {
        _selectedTopic = node;
        _isLoading = false;
      });
    }

    ReturnToList() : void
    {
      _selectedTopic = null;
    }
    
    [Html]
    public View() : string
    {
      <#
        <h2 class="topic-list-name" click="$ReturnToList">$Name</h2>
        <table $when(_selectedTopic == null && (_topics != null && _topics.Count > 0)) class="topic-list">
          <tr>
            <th colspan="2">Сообщения</th>
            <th>Автор</th>
            <th>#</th>
            <th colspan="2">Ответы</th>
          </tr>
          <tr $foreach(topic in _topics) class="topic" click="$(OpenTopic(topic))">
            <td>
              <img $when(topic.Comments > 0) src="http://rsdn.ru/Forum/images/fr1.gif" />
              <img $when(topic.Comments == 0) src="http://rsdn.ru/Forum/images/fr0.gif" />
            </td>
            <td class="topic-name">
              $(topic.Name)
            </td>
            <td>
              $(topic.Author)
            </td>
            <td>
              <span $when(topic.Rating > 0)>
                $(topic.Rating)                
              </span>
              <span $when(topic.Rating == 0 && (topic.Agrees > 0 || topic.Disagrees > 0))>
                +/-
              </span>
            </td>
            <td>
              $(topic.Comments)
            </td>
            <td>              
              $(topic.LastUpdatedOn)
            </td>
          </tr>
        </table>
        <div $when(_selectedTopic != null)>
          <div template="$_selectedTopic" />
        </div>
        <div class="loading" visible="$_isLoading">Loading...</div>
      #>
    }
    
    public class Server
    {
      public GetTopicList(url : string) : IEnumerable[Topic]
      {
        TopicLoader.GetTopicList(url);
      }

      public LoadReplies(id : int) : CommentNode
      {
        TopicLoader.LoadReplies(id)
      }
    }
  }
}
