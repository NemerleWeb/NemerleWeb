using System.Collections.Generic;
using System.Linq;
using Nemerle.Macros;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Collections;
using System.IO;
using System.Text.RegularExpressions;
using TSParser;
using NemerleWeb;

namespace NemerleWeb.Macros
{
  [Nemerle.MacroUsage(Nemerle.MacroPhase.BeforeInheritance, Nemerle.MacroTargets.Assembly)]
  macro GenerateJSApi(params options : list[PExpr])
  {
    GenerateJSApiImpl.Impl(ImplicitCTX(), options)
  }
  
  module GenerateJSApiImpl
  {
    tsParser = TSDeclarationParser();
    
    public Impl(typer : Typer, options : list[PExpr]) : void
    {      
      match(options)
      {
      | [<[ Directory = $(directory : string) ]> ] with (libPath = "", exclude = "")
      | [<[ Directory = $(directory : string) ]>,
         <[ Exclude = $(exclude : string) ]> ] with libPath = ""
      | [<[ Lib = $(libPath : string) ]>,
         <[ Directory = $(directory : string) ]> ] with exclude = ""
      | [<[ Lib = $(libPath : string) ]>,
         <[ Directory = $(directory : string) ]>,
         <[ Exclude = $(exclude : string) ]> ] =>
        DoImpl(typer, libPath, directory, exclude)
      | _ =>
        Message.Error("Specify Directory parameter and optional Lib, Exclude parameters");
      }
    }
    
    private DoImpl(typer : Typer, libPath : string, directory : string, exclude : string) : void
    {
      def projectDir = Path.GetDirectoryName(typer.Manager.Options.ProjectPath);
      
      // PreParse
      // Resolve all references
      def re = Regex(exclude);
      def isIncluded(s) { if (exclude == "") true else !re.IsMatch(s) }
      
      def fullDir = Path.Combine(projectDir, directory);
      def tsFiles = Directory.EnumerateFiles(fullDir, "*.d.ts", SearchOption.AllDirectories);

      def tsFilesToParse = HashSet();
      foreach(tsFile when isIncluded(tsFile) in tsFiles)
        PreParseTS(tsFile, tsFilesToParse);
      
      // Parsing
      
      // Parse lib if present
      when (libPath != "")
        ParseTS(typer, Path.Combine(projectDir, libPath), GetNamespaceName(directory), "");
      
      foreach(tsFile in tsFilesToParse)
      {
        // TODO: Support references outside our directory
        when (tsFile.Length < fullDir.Length || tsFile.Substring(0, fullDir.Length) != fullDir)
          Message.Error($"File $tsFile is outside directory $directory");
        
        def baseNamespace = GetNamespaceName(Path.GetDirectoryName(tsFile).Substring(fullDir.Length + 1));
        def fileName = Path.GetFileNameWithoutExtension(Path.GetFileNameWithoutExtension(tsFile));
        
        //ParseTS(typer, tsFile, baseNamespace, fileName);
      }
    }
     
    /// Parse all references
    private PreParseTS(tsFile : string, tsFilesToParse : HashSet[string]) : void
    {
      if(tsFilesToParse.Contains(tsFile)) ()
      else
        match(tsParser.TryParse(File.ReadAllText(tsFile)))
        {
        | (pos, result) when pos == tsParser.ParsingSource.OriginalText.Length =>
          // Add file
          _ = tsFilesToParse.Add(tsFile);
        
          // Check all references
          foreach(TSGlobal.ReferencePath(path) in result)
          {
            def reference = Path.GetFullPath(Path.Combine(Path.GetDirectoryName(tsFile), path));
            _ = tsFilesToParse.Add(reference);
          
            PreParseTS(reference, tsFilesToParse);
          }
        | (pos, _) =>
          // TODO: Add locations to parser and print here correct location in the file.
          def text = tsParser.ParsingSource.OriginalText;
          Message.Error(Location(tsFile, 0, 0, 0, 0), $"Failed to parse $tsFile, somewhere here: $(text.Substring(pos, 55))...");
        }
    }
    
    private ParseTS(typer : Typer, tsFile : string, baseNamespace : string, fileName : string) : void
    {
      match(tsParser.TryParse(File.ReadAllText(tsFile)))
      {
      | (pos, result) when pos == tsParser.ParsingSource.OriginalText.Length =>       
        // Root JS API namespace
        def jsApiRoot = typer.Env.EnterIntoNamespace(["JSApi"]);
        
        // Create module and namespace
        def fullRelativeApi = $"$baseNamespace.$fileName".TrimEnd('.');
        def jsApiNamespace = jsApiRoot.EnterIntoNamespace(fullRelativeApi.SplitDot());
        def jsApiModule = CreateModule(fullRelativeApi, jsApiRoot);
    
        // Generate members
        foreach(r in result) 
        {
        | Variable  as v => CreateTSGlobalVariable (v, jsApiModule)
        | Function  as f => CreateTSGlobalFunction (f, jsApiModule)
        | Interface as i => CreateTSGlobalInterface(i, jsApiNamespace)
        | Class     as c => CreateTSGlobalClass    (c, jsApiNamespace)
        | Construct as c => CreateTSGlobalConstruct(c, jsApiModule)
        | Enum      as e => CreateTSGlobalEnum     (e, jsApiNamespace)
        | Import    as i => CreateTSGlobalImport   (i, jsApiNamespace)
        | Export    as e => CreateTSGlobalExport   (e, jsApiNamespace)
        | Module    as m => CreateTSGlobalModule   (m, jsApiNamespace)
        | _ => ()
        }
    
        when(fileName != "") jsApiModule.Compile();
        
      | (pos, _) =>
        // TODO: Add locations to parser and print here correct location in the file.
        def text = tsParser.ParsingSource.OriginalText;
        Message.Error(Location(tsFile, 0, 0, 0, 0), $"Failed to parse $tsFile, somewhere here: $(text.Substring(pos, 55))...")
      }
    }
 
    private CreateTSGlobalVariable(var : TSGlobal.Variable, jsApi : TypeBuilder) : void
    {
      def variableType = var.Name + "AnonymousClass";
      jsApi.DefineNestedType(<[ decl: public partial class $(variableType : usesite) {} ]>).Compile();
      
      // DefineMembers(anon, var.Members);
      
      //_ = jsApi.Define(<[ decl: public $(var.Name : usesite) : $(variableType : usesite) ]>);
    }
  
    private CreateTSGlobalFunction(function : TSGlobal.Function, jsApi : TypeBuilder) : void
    {
      if (function.Name is Some(fname))
      {
        jsApi.Define(<[ decl: 
          $(fname : usesite)() : $(ConvertType(function.ReturnType))
          {
            throw ClientCodeCalledInServerException();
          }
        ]>);
      }
      else
      {
        Message.Error("Global functions must have name");
      }
    }
  
    private CreateTSGlobalInterface(tsInterface : TSGlobal.Interface, jsApiNamespace : GlobalEnv) : void
    {
      def type =
        if (tsInterface.Extends is [])
          <[ decl: public partial interface $(tsInterface.Name : usesite) {} ]>
        else
        {
          def extends = tsInterface.Extends.Map(e => <[ $(e : usesite) ]>);
          <[ decl: public partial interface $(tsInterface.Name : usesite) : ..$extends {} ]>
        };
      jsApiNamespace.Define(type).Compile();
    }
  
    private CreateTSGlobalClass(tsClass : TSGlobal.Class, jsApiNamespace : GlobalEnv) : void
    {
      def type = 
        match((tsClass.Extends, tsClass.Implements))
        {
        | (None, []) =>
          <[ decl: public partial class $(tsClass.Name : usesite) {} ]>
        | _ =>
          def extends = if (tsClass.Extends is Some(ex)) [<[ $(ex : usesite) ]>] else [];
          def implement = tsClass.Implements.Map(e => <[ $(e : usesite) ]>);
          def inherit = extends.Append(implement);
          <[ decl: public partial class $(tsClass.Name : usesite) : ..$inherit {} ]>
        };
      jsApiNamespace.Define(type).Compile();
    }
  
    private CreateTSGlobalConstruct(c : TSGlobal.Construct, jsApi : TypeBuilder) : void
    {
      
    }
    
    private CreateTSGlobalEnum(tsEnum : TSGlobal.Enum, jsApiNamespace : GlobalEnv) : void
    {
      def type = <[ decl: public enum $(tsEnum.Name : usesite) { } ]>;
      jsApiNamespace.Define(type).Compile();
    }
  
    private CreateTSGlobalImport(i : TSGlobal.Import, jsApiNamespace : GlobalEnv) : void
    {
      
    }
  
    private CreateTSGlobalExport(e : TSGlobal.Export, jsApiNamespace : GlobalEnv) : void
    {
      
    }
  
    private CreateTSGlobalModule(tsModule : TSGlobal.Module, jsApiNamespace : GlobalEnv) : void
    {
      def moduleName = tsModule.Name.Replace('/', '.').Replace('\\', '.');
      
      // Create modules by splitting '.'
      _ = CreateModule(moduleName, jsApiNamespace);
    }

    #region Helpers
    private GetParamsType(parms : list[TSParam], additional : option[TSType]) : list[PExpr]
    {
      def convertParamsType(p : TSParam)
      {
        | Required(_, t, _)
        | Option(_, _, t, _) => ConvertType(t)
        | Rest(_, t) => ConvertType(t)
      }
    
      def ret = 
        parms
          .Map(convertParamsType)
          .Append(if (additional is Some(t)) [ConvertType(t)] else []);
    
      ret
    }
  
    private ConvertType(inputType : option[TSType]) : PExpr
    {
      | Some(type) => ConvertType(type)
      | None => <[ object ]>
    }
  
    private ConvertType(inputType : TSType) : PExpr
    {
      | Array(type, 0) => <[ $(ConvertType(type)) ]>
      | Array(type, arity) => <[ array[$(ConvertType(TSType.Array(type, arity - 1)))] ]>
      | Bool => <[ bool ]>    
      | Lambda(parms, ret) =>
        def (type, additional) = 
          if (ret is TSType.Void()) (<[ Action ]>, None())
          else                      (<[ Func ]>, Some(ret));
        
        <[ $type.[{..$(GetParamsType(parms, additional))}] ]>
      | Named
      | New
      | Number => <[ float ]>
      | Object => Message.Hint("Object is not supported"); <[ ]>
      | String => <[ void ]>
      | Void => <[ void ]>
      | Any
      | _ => <[ object ]>
    }
        
    /// Replace path separator with dots
    private GetNamespaceName(path : string) : string
    {
      path.Replace('\\', '.').Replace('/', '.').TrimEnd('.')
    }
    
    private CreateModule(parts : list[string], outer : TypeBuilder) : TypeBuilder
    {
      match (parts)
      {
      | [] => outer
      | [hd] =>
        def t = outer.DefineNestedType(<[ decl: public partial module $(hd : usesite) {} ]>);
        t.Compile();
        t
      | hd :: tl =>
        def innerModule = outer.DefineNestedType(<[ decl: public partial module $(hd : usesite) {} ]>);
        def t = CreateModule(tl, innerModule);
        innerModule.Compile();
        t
      | _ => Message.Error("Must not get there"); null
      }
    }
    
    private CreateModule(moduleName : string, outer : TypeBuilder) : TypeBuilder
    {
      CreateModule(moduleName.SplitDot(), outer)
    }
    
    private CreateModule(moduleName : string, outer : GlobalEnv) : TypeBuilder
    {
      def parts = moduleName.SplitDot();
      match (parts)
      {
      | [] => Message.Error("Empty list"); null
      | [hd] =>
        def t = outer.Define(<[ decl: public partial module $(hd : usesite) {} ]>);
        t.Compile();
        t
      | hd :: tl =>
        def innerModule = outer.Define(<[ decl: public partial module $(hd : usesite) {} ]>);
        def t = CreateModule(tl, innerModule);
        innerModule.Compile();
        t
      | _ => Message.Error("Must not get there"); null
      }
    }
    
    #endregion
  }
  
  module StringExt
  {
    public SplitDot(this s : string) : list[string]
    {
      s.Split('.').NToList()
    }
  }
}
