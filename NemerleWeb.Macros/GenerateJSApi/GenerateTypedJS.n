using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Macros;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Assertions;
using System.IO;
using System.Text.RegularExpressions;
using TSParser;
using NemerleWeb;

namespace NemerleWeb
{
  /// <summary>
  /// Parses d.ts files and creates an appropriate Nemerle types and variables.
  ///
  /// Macro parameters:
  ///   Namespace: Required argument, where all classes will be generated.
  ///   Lib (optional): Where lib.d.ts file exists.
  ///   Files:
  ///     First: Directory.
  ///     Second: Exclude (optional). Do not look for this files. Specified by regular expression.  
  /// Path is relative to the project directory.
  ///
  /// Example, assuming we have all files in TS directory:
  ///   [assembly: GenerateJSApi(
  ///     Namespace = "TypedJS"
  ///     Lib = @"TS\_infrastructure\1.0\lib.d.ts",
  ///     Files =
  ///     [
  ///       ("TS", @"TS\\_infrastructure\\"),
  ///       ("UserTSD")
  ///     ]
  ///   )]
  ///
  /// The convertion:
  /// All types are generated in the given namespace.
  /// The namespace and module with corresponding name are created.  
  /// This allows to have one 'using' for global JS functions and types.
  ///
  /// First all files are preparsed to resolve all referenced files.
  /// Then all files are parsed and code is generated.
  /// Types (classes, interfaces, modules) are generated before variables and functions.
  ///
  ///
  ///
  ///
  ///
  ///
  ///
  ///
  ///
  ///
  /// </summary>
  [Nemerle.MacroUsage(Nemerle.MacroPhase.BeforeInheritance, Nemerle.MacroTargets.Assembly)]
  macro GenerateTypedJS(params options : list[PExpr])
  {
    GenerateTypedJSImpl.Impl(ImplicitCTX(), options)
  }
  
  [Record]
  class DirectoryArg
  {    
    public Directory : string { get; }
    public Exclude : string { get; }
  }
  
  [Record]
  class GenerateArgs
  {
    public Root : string { get; }
    public Lib : string { get; }
    public Args : IEnumerable[DirectoryArg] { get; }
  }
  
  module GenerateTypedJSImpl
  {
    #region Implementation
    
    tsParser = TSDeclarationParser();
    
    public Impl(typer : Typer, options : list[PExpr]) : void
    {
      DefineCTX(typer);
      
      def args = ParseArgs(options);
      
      // Add assembly level attribute
      Manager().Hierarchy.AddAssemblyAttribute(
        Manager().CoreEnv,
        <[ NemerleWeb.JSApiRootAttribute($(args.Root : string)) ]>);
      
      // Step 1: PreParse
      def projectDir = Path.GetDirectoryName(typer.Manager.Options.ProjectPath);
      def tsFilesToParse = PreParseTSAll(projectDir, args.Lib, args.Args);
      
      // Step 2: Parse
      def tsAsts = tsFilesToParse.SelectMany(ParseTS);
      
      // Step 3: Analyze
      def finalAst = AnalyzeTSAst(tsAsts);
      
      // Step 4: Generate
      GenerateCode(typer, args.Root, finalAst);
    }
    
    private ParseArgs(options : list[PExpr]) : GenerateArgs
    {
      match(options)
      {
      | [<[ Root = $(root : string) ]>, <[ Files = {..$files} ]> ] with lib = ""
      | [<[ Root = $(root : string) ]>, <[ Lib = $(lib : string) ]>, <[ Files = [..$files] ]>] =>
        // Convert PExpr sequence to regular type
        def filesArg = files.Select(
          f => match(f)
          {
          | <[ $(dir : string) ]> with exclude = ""
          | <[ ($(dir : string), $(exclude : string)) ]> => DirectoryArg(dir, exclude)
          | _ =>
            Message.Error("You must specify at most 2 arguments: directory and exclude pattern");
            null
          });
        GenerateArgs(root, lib, filesArg)
      | _ =>
        Message.Error("Specify Root, Files parameter and optional Lib");
        null
      }
    }
    
    #endregion
    
    #region PreParse
    
    private PreParseTSAll(projectDir : string, lib : string, args : IEnumerable[DirectoryArg]) : HashSet[string]
    {      
      def tsFilesToParse = HashSet();
      
      // Add lib if exists
      when(lib != "") _ = tsFilesToParse.Add(Path.Combine(projectDir, lib));
      
      // Resolve all references        
      foreach(arg in args)
      {
        def re = Regex(arg.Exclude);
        def isIncluded(s) { if (arg.Exclude == "") true else !re.IsMatch(s) }
      
        def fullDir = Path.Combine(projectDir, arg.Directory);          
        try
        {
          def tsFiles = Directory.EnumerateFiles(fullDir, "*.d.ts", SearchOption.AllDirectories);

          foreach(tsFile when isIncluded(tsFile) in tsFiles)
            PreParseTS(tsFile, tsFilesToParse);
        }
        catch
        {
          | _ => Message.Hint($"Directory \"$fullDir\" doesn't exist. Skipping.");
        }
      }
      
      tsFilesToParse
    } 
    
    private PreParseTS(tsFile : string, tsFilesToParse : HashSet[string]) : void
    {
      when (tsFilesToParse.Add(tsFile))
        match(tsParser.TryParse(File.ReadAllText(tsFile)))
        {
        | (pos, result) when pos == tsParser.ParsingSource.OriginalText.Length =>
          // Add file
          _ = tsFilesToParse.Add(tsFile);
        
          // Check all references
          foreach(TSGlobal.ReferencePath(path) in result)
          {
            def reference = Path.GetFullPath(Path.Combine(Path.GetDirectoryName(tsFile), path));
            _ = tsFilesToParse.Add(reference);
          
            PreParseTS(reference, tsFilesToParse);
          }
        | (pos, _) =>
          // TODO: Add locations to parser and print here correct location in the file.
          def text = tsParser.ParsingSource.OriginalText;
          Message.Error(Location(tsFile, 0, 0, 0, 0), $"Failed to parse $tsFile, somewhere here: $(text.Substring(pos, 55))...");
        }
    }
    
    #endregion
    
    #region Parse
    
    private ParseTS(tsFile : string) : IEnumerable[TSGlobal]
    {
      match(tsParser.TryParse(File.ReadAllText(tsFile)))
      {
      | (pos, result) when pos == tsParser.ParsingSource.OriginalText.Length =>
        result
      | (pos, _) =>
        // TODO: Add locations to parser and print here correct location in the file.
        def text = tsParser.ParsingSource.OriginalText;
        Message.Error(Location(tsFile, 0, 0, 0, 0), $"Failed to parse $tsFile, somewhere here: $(text.Substring(pos, 55))...");
        null
      }
    }
    
    #endregion
    
    #region Analyze
    
    /// <summary>
    /// Take all elements and remove duplicates
    /// </summary>
    private AnalyzeTSAst(tsAsts : IEnumerable[TSGlobal]) : IEnumerable[TSGlobal]
    {
      def filter(ast)
      {
        | TSGlobal.Variable 
        | TSGlobal.Construct
        | TSGlobal.Function
        | TSGlobal.Class 
        | TSGlobal.Interface
        | TSGlobal.Module
        | TSGlobal.Enum => true
        | TSGlobal.Import 
        | TSGlobal.Export
        | _ => false
      }     
      def group(ast)
      {
        def name =
          match(ast)
          {
          | TSGlobal.Variable  as v => v.Name
          | TSGlobal.Construct      => ""
          | TSGlobal.Function  as f => f.Name ?? ""
          | TSGlobal.Class     as c => c.Name
          | TSGlobal.Interface as i => i.Name
          | TSGlobal.Module    as m => m.Name
          | TSGlobal.Enum      as e => e.Name
          | _ => throw Exception(); ""
          };
        // Group by type and name
        (ast.GetType(), name)
      }
      def select(typeAndNameGroup)
      {
        def type = typeAndNameGroup.Key[0];
        
        // TODO: Either compiler should support for type matching or add support in ExtensionPattern
        if(type.Equals(typeof(TSGlobal.Variable)))
        {
          // Return first variable and skip other
          [typeAndNameGroup.First()]
        }
        else
        if(type.Equals(typeof(TSGlobal.Function)))
        {
          [typeAndNameGroup.First()]
        }
        else
        {
          [typeAndNameGroup.First()]
        }
      }
      
      tsAsts.Where(filter).GroupBy(group).SelectMany(select)
    }
    
    #endregion
    
    #region Generate
    
    GenerateCode(typer : Typer, root : string, tsAst : IEnumerable[TSGlobal]) : void
    {
      def jsApiNamespace = typer.Env.EnterIntoNamespace(root.SplitDot());
      def jsApiModule = CreateModule(root, Scope(typer.Env));
      
      // Generate members
      foreach(e in tsAst) 
      {
      | Interface as i => CreateTSGlobalInterface(i, jsApiNamespace)
      | Class     as c => CreateTSGlobalClass    (c, jsApiNamespace)
      | Enum      as e => CreateTSGlobalEnum     (e, jsApiNamespace)
      | Import    as i => CreateTSGlobalImport   (i, jsApiNamespace)
      | Export    as e => CreateTSGlobalExport   (e, jsApiNamespace)
      | Module    as m => CreateTSGlobalModule   (m, jsApiNamespace)
      | _ => ()
      }
      
      foreach(e in tsAst)
      {
      | Variable  as v => CreateTSGlobalVariable (v, jsApiModule, jsApiNamespace)
      | Function  as f => CreateTSGlobalFunction (f, jsApiModule, jsApiNamespace)
      | Construct as c => CreateTSGlobalConstruct(c, jsApiModule)
      | _ => ()
      }
      
      jsApiModule.Compile();
    }
 
    private CreateTSGlobalVariable(var : TSGlobal.Variable, jsApi : TypeBuilder, jsApiNamespace : GlobalEnv) : void
    {
      def typePrefix = var.Name + "_Variable";
      def varType = ConvertType(typePrefix, var.Type, jsApiNamespace);
      // TODO: Remove check
      when(!varType.ToString().Contains("."))
        _ = jsApi.Define(<[ decl: public $(var.Name : usesite) : $varType ]>);
    }
  
    /// <summary>
    /// Creates function in module.
    /// Global function can be without a name.
    /// </summary>
    private CreateTSGlobalFunction(function : TSGlobal.Function, jsApi : TypeBuilder, jsApiNamespace : GlobalEnv) : void
    {
      // TODO: Remove check
      // TODO: Add parameters
      when(!function.ReturnType.ToString().Contains("."))
      {
        def typePrefix = function.Name + "_Return";
        def returnType = ConvertType(typePrefix, function.ReturnType, jsApiNamespace);
        
        _ = jsApi.Define(<[ decl: 
          $(function.Name : usesite)() : $returnType
          {
            throw ClientCodeCalledInServerException();
          }
        ]>);
      }      
    }
    
    private CreateTSGlobalInterface(tsInterface : TSGlobal.Interface, jsApiNamespace : GlobalEnv) : void
    {
      def type =
        if (tsInterface.Extends is [])
          <[ decl: public partial interface $(tsInterface.Name : usesite) {} ]>
        else
        {
          def extends = tsInterface.Extends.Select(e => <[ $(e : usesite) ]>);
          <[ decl: public partial interface $(tsInterface.Name : usesite) : ..$extends {} ]>
        };
        
      jsApiNamespace.Define(type).Compile();
    }
  
    private CreateTSGlobalClass(tsClass : TSGlobal.Class, jsApiNamespace : GlobalEnv) : void
    {
      def type = 
        match((tsClass.Extends, tsClass.Implements))
        {
        | (None, []) =>
          <[ decl: [JSApi] public partial class $(tsClass.Name : usesite) {} ]>
        | _ =>
          def extends = if (tsClass.Extends is Some(ex)) [<[ $(ex : usesite) ]>] else [];
          def implement = tsClass.Implements.Select(e => <[ $(e : usesite) ]>);
          def inherit = extends.Concat(implement);
          <[ decl: [JSApi] public partial class $(tsClass.Name : usesite) : ..$inherit {} ]>
        };
        
      jsApiNamespace.Define(type).Compile();
    }
  
    private CreateTSGlobalConstruct(c : TSGlobal.Construct, jsApi : TypeBuilder) : void
    {
      
    }
    
    private CreateTSGlobalEnum(tsEnum : TSGlobal.Enum, jsApiNamespace : GlobalEnv) : void
    {
      def type = <[ decl: public enum $(tsEnum.Name : usesite) { } ]>;
      jsApiNamespace.Define(type).Compile();
    }
  
    private CreateTSGlobalImport(i : TSGlobal.Import, jsApiNamespace : GlobalEnv) : void
    {
      
    }
  
    private CreateTSGlobalExport(e : TSGlobal.Export, jsApiNamespace : GlobalEnv) : void
    {
      
    }
  
    private CreateTSGlobalModule(tsModule : TSGlobal.Module, jsApiNamespace : GlobalEnv) : void
    {
      def moduleName = tsModule.Name.Replace('/', '.').Replace('\\', '.');
      
      // Create modules by splitting '.'
      _ = CreateModule(moduleName, Scope(jsApiNamespace));
    }
    
    #endregion

    #region Helpers
    
    private GetParamsType(prefix : string, parms : IEnumerable[TSParam], jsApiNamespace : GlobalEnv) : IEnumerable[PExpr]
    {
      def makePrefix(name)
      {
        $"$(prefix)_$(name)_Parameter"
      }
      def convertParamsType(p : TSParam)
      {
        | Required(name, type, _access)
        | Option(name, _, type, _access) => ConvertType(makePrefix(name), type, jsApiNamespace)
        | Rest(name, type) => ConvertType(makePrefix(name), type, jsApiNamespace)
      }
    
      def ret = parms.Select(convertParamsType);
      ret
    }
    
    private CreateAnonymousType(prefix : string, members : list[TSObject], jsApiNamespace : GlobalEnv) : PExpr
    {
      def className = $"$(prefix)_AnonymousType";
      def anonClass = jsApiNamespace.Define(<[ decl: 
        public sealed class $(className : usesite)
        {
        }
      ]>);
      
      // Add members
      foreach(member in members)
      {
      | Call(parms, returnType) =>
        def returnTypePrefix = $"$(prefix)_Call_Return";
        def realReturnType = ConvertType(returnTypePrefix, returnType, jsApiNamespace);

        if(parms is [])        
        _ = anonClass.Define(
          <[ decl:
            public static @()(this _ : this) : $realReturnType
            {
              throw ClientCodeCalledInServerException();
            }
          ]>);
        else
        _ = anonClass.Define(
          <[ decl:
            public static @()(this _ : this, _ : object) : $realReturnType
            {
              throw ClientCodeCalledInServerException();
            }
          ]>);
      | Construct(parms, type) =>
        ()
      | Index(paramName, paramType, returnType) =>
        def (realParamType, typeName) =
          match(paramType)
          {
          | Number => (<[ float ]>, "Number")
          | String => (<[ string ]>, "String")
          };
        
        def returnTypePrefix = $"$(prefix)_$(typeName)_Index_Return";
        def realReturnType = ConvertType(returnTypePrefix, returnType, jsApiNamespace);
        
        _ = anonClass.Define(
          <[ decl:
            public Item[$(paramName : usesite) : $realParamType] : $realReturnType
            {
              get
              {
                throw ClientCodeCalledInServerException();
              }
              set
              {
                throw ClientCodeCalledInServerException();
              }
            }
          ]>);
      
      | Property(name, _optional, type)  =>
        // Don't pay attention to optional, always generate property      
        def typePrefix = $"$(prefix)_$(name)_Property";
        def propertyType = ConvertType(typePrefix, type, jsApiNamespace);
        
        _ = anonClass.Define(<[ decl: public mutable $(name : usesite) : $propertyType; ]>);
      
      | Function(name, _optional, parms, returnType) =>
        // Don't pay attention to optional, always generate property      
        def returnTypePrefix = $"$(prefix)_$(name)_Return";
        def returnType = ConvertType(returnTypePrefix, returnType, jsApiNamespace);
        
        _ = anonClass.Define(
          <[ decl: 
            $(name : usesite)() : $returnType
            {
              throw ClientCodeCalledInServerException();
            }
          ]>);
      }
      
      // Finalize
      anonClass.Compile();
      <[ $(anonClass.Name : usesite) ]>
    }
  
    private ConvertType(prefix : string, inputType : option[TSType], jsApiNamespace : GlobalEnv) : PExpr
    {
      match(inputType)
      {
      | Some(type) => ConvertType(prefix, type, jsApiNamespace)
      | None => <[ object ]>
      }
    }
  
    private ConvertType(prefix : string, inputType : TSType, jsApiNamespace : GlobalEnv) : PExpr
    {
      match(inputType)
      {
      | Array(type, 0) => <[ $(ConvertType(prefix, type, jsApiNamespace)) ]>
      | Array(type, arity) => <[ array[$(ConvertType(prefix, TSType.Array(type, arity - 1), jsApiNamespace))] ]>
      | Bool => <[ bool ]>   
      | Lambda(parms, ret) =>
        mutable lambdaParams = GetParamsType(prefix, parms, jsApiNamespace);
        def type =
          if(ret is TSType.Void())
            <[ System.Action ]>
          else
          {
            lambdaParams = lambdaParams.Concat([ConvertType(prefix, ret, jsApiNamespace)]);
            <[ System.Func ]>
          };
        
        <[ $type.[..$lambdaParams] ]>
      | Named(name) => <[ $(name : usesite) ]>
      | New => Message.Hint("new() is not supported yet"); <[ object ]>
      | Number => <[ float ]>
      | Object(members) => CreateAnonymousType(prefix, members, jsApiNamespace)
      | String => <[ string ]>
      | Void => <[ void ]>
      | Any => <[ object ]>
      }
    }
    
    /// <summary>
    /// This method is needed because of bug in Nemerle compiler which cannot choose nearest symbol.
    /// To workaround this behaviour we always specify fully qualified name.    
    ///
    /// module A
    /// {
    ///    class X{}
    ///    module B
    ///    {
    ///       class X{}
    ///       class Q
    ///       {
    ///           x : X; // Ambibguity
    ///       }
    ///    }
    /// }
    /// </summary>
    GetFullyQualifiedName(name : string, jsApiNamespace : GlobalEnv) : string
    {
      def lookupType(name, node)
      {
        match(node.LookupType(name.SplitDot(), 0))
        {
        | Some(t) => t.FullName
        | _ =>
          if(node.GetDisplayName() == "JSApi")
            name
          else
            lookupType(name, node.Parent)
        }
      }
      
      lookupType(name, jsApiNamespace.CurrentNamespace);
    }
         
    /// Replace path separator with dots
    private GetNamespaceName(path : string) : string
    {
      path.Replace('\\', '.').Replace('/', '.').TrimEnd('.')
    }
    
    private CreateModule(moduleName : string, outer : Scope) : TypeBuilder
    {
      CreateModule(moduleName.SplitDot(), outer)
    }    
    
    private CreateModule(parts : list[string], outer : Scope) : TypeBuilder
    {
      match (parts)
      {
      | [] => 
        if(outer.TypeBuilder != null) outer.TypeBuilder 
        else
        {
          Message.Error("List must not be empty");
          null
        }
      | [hd] =>
        def t = outer.Define(<[ decl: public partial module $(hd : usesite) {} ]>);
        t.Compile();
        t
      | hd :: tl =>
        def innerModule = outer.Define(<[ decl: public partial module $(hd : usesite) {} ]>);
        def t = CreateModule(tl, Scope(innerModule));
        innerModule.Compile();
        t
      | _ => Message.Error("Must not get there"); null
      }
    }
    
    #endregion
  
    public SplitDot(this s : string) : list[string]
    {
      s.Split('.').NToList()
    }
  }
}
