using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Late;

using System;
using System.Web;
using System.Globalization;
using System.Collections.Generic;
using System.Linq;
using System.Text.RegularExpressions;
using BF = System.Reflection.BindingFlags;
using TExpr = Nemerle.Compiler.Typedtree.TExpr;

using Microsoft.AspNet.SignalR;
using Microsoft.AspNet.SignalR.Hubs;

namespace NemerleWeb.Macros
{
  macro signal_macro(expr) 
  syntax("signal", expr)
  {
    def ctx = Macros.ImplicitCTX();
    SignalImpl.TransformSignal(expr, ctx);
  }
  
  macro broadcast_macro(expr) 
  syntax("broadcast", expr)
  {
    def ctx = Macros.ImplicitCTX();
    SignalImpl.TransformBroadcast(expr, ctx);
  }
  
  public module SignalImpl
  {
    private Transform(expr : PExpr, typer : Typer, objExpr : PExpr) : option[PExpr]
    {
      match(expr)
      {
      | <[ $_.$method(..$parms) ]> =>
        def hubName = typer.CurrentTypeBuilder.DeclaringType.Name + "Hub";
      
        Some(<[
          def signalClients =
            GlobalHost.ConnectionManager.GetHubContext.[$(hubName : dyn)]().Clients;        
            
          $objExpr;
          
          def invokeMethod = obj.GetType().GetMethod(
            "Invoke",
            BindingFlags.NonPublic | BindingFlags.Public | BindingFlags.Instance);
          
          _ = invokeMethod.Invoke(obj, array[$(method.ToString() : string) : object, array[..$parms]]);
        ]>)   
      | _ => None()
      }
    }
    
    public TransformSignal(expr : PExpr, typer : Typer) : PExpr 
    {
      def res = Transform(
        expr,
        typer, 
        <[
            def signalConnectionId = HttpContext.Current.Session["SignalRConnectionId"];
            def obj = signalClients.Client(signalConnectionId);
        ]>);
      match (res)
      {
      | Some(p) => p
      | _ => Message.FatalError(expr.Location, "Invalid 'signal' macro call. Syntax: signal client.MethodName(parameters)");
      }
    }
    
    public TransformBroadcast(expr : PExpr, typer : Typer) : PExpr 
    {
            
      def res = Transform(expr, typer, <[ def obj = late signalClients.All; ]>);
      match(res)
      {
      | Some(p) => p
      | _ => Message.FatalError(expr.Location, "Invalid 'broadcast' macro call. Syntax: broadcast client.MethodName(parameters)");
      }
    }
  }
}