using System;
using System.Globalization;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Extensions;
using Nemerle.Utility;

namespace Nemerle.Peg
{
  [StructuralHashCode, StructuralEquality]
  public class RangeSet
  {//TODO:Optimize
    [Record, StructuralHashCode, StructuralEquality]
    public struct Range
    {
      public from : char;
      public to : char;

      public override ToString() : string
      {
        def esc(ch)
        {
          | '\0' => @"'\0'"
          | '\t' => @"'\t'"
          | '\n' => @"'\n'"
          | '\r' => @"'\r'"
          | '\\' => @"'\\'"
          | '\'' => @"'\''"
          | x when x == char.MaxValue  => @" char.MaxValue"
          | c when char.IsLetter(c)
          | c when char.IsDigit(c)
          | c when char.IsPunctuation(c)
          | c when char.IsSeparator(c)
          | c when char.IsSymbol(c)      => $"'$c'"
          | c =>
            def c = c : uint;
            string.Format(<#'\u{0,4:X}'#>, c).Replace(' ', '0');
        }

        if (from == to) esc(from)
        else            esc(from) + ".." + esc(to)
      }
    }

    [Accessor]
    private _ranges : list[Range] = [];

    public this() {}

    private this(ranges : list[Range])
    {
      _ranges = ranges;
    }

    public AddRange(from : char, to : char) : RangeSet
    {
      AddRange(Range(from, to));
    }

    public AddRange(range : Range) : RangeSet
    {
      assert(range.from <= range.to);

      def add(ranges, range)
      {
        match (ranges)
        {
        | [] => [range];
        | firstRange :: ranges =>
          if (firstRange.to + 1 < range.from)
            firstRange :: add(ranges, range);
          else if (range.to + 1 < firstRange.from)
            range :: firstRange :: ranges;
          else
            add(ranges, Range(Math.Min(range.from, firstRange.from) :> char, Math.Max(range.to, firstRange.to) :> char));
        }
      }
      RangeSet(add(_ranges, range))
    }

    public RemoveRange(from : char, to : char) : RangeSet
    {
      RemoveRange(Range(from, to));
    }

    public RemoveRange(range : Range) : RangeSet
    {
      assert(range.from <= range.to);

      def remove(ranges)
      {
        match (ranges)
        {
        | [] => [];
        | firstRange :: ranges =>
          if (firstRange.to < range.from)
          {
            firstRange :: remove(ranges);
          }
          else if (firstRange.from > range.to)
          {
            firstRange :: ranges;
          }
          else
          {
            def cat(from : int, to : int, ranges)
            {
              if (from <= to)
                Range(from :> char, to :> char) :: ranges;
              else
                ranges;
            }
            def lo = firstRange.from <= range.from;
            def hi = firstRange.to >= range.to;
            if (lo)
            {
              if (hi)
              {
                cat(firstRange.from, range.from - 1, cat(range.to + 1, firstRange.to, ranges))
              }
              else
              {
                cat(firstRange.from, range.from - 1, remove(ranges))
              }
            }
            else
            {
              if (hi)
              {
                cat(range.to + 1, firstRange.to, ranges)
              }
              else
              {
                remove(ranges);
              }
            }
          }
        }
      }
      RangeSet(remove(_ranges))
    }

    public Sum(that : RangeSet) : RangeSet
    {
      Ranges.Fold(that, (range, that) => that.AddRange(range));
    }

    public Sub(that : RangeSet) : RangeSet
    {
      that.Ranges.Fold(this, (range, rangeSet) => rangeSet.RemoveRange(range));
    }

    public Intersect(that : RangeSet) : RangeSet
    {
      this.Sum(that).Sub(this.Sub(that)).Sub(that.Sub(this))
    }

    public Invert() : RangeSet
    {
      RangeSet([Range(char.MinValue, char.MaxValue)]).Sub(this)
    }

    public IsSinglChar : bool
    {
      get
      {
        match (Ranges)
        {
          | [x] when x.from == x.to => true
          | _                       => false
        }
      }
    }

    public override ToString() : string { $"..$_ranges" }
  }
}
