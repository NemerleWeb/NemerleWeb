using System;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Parsetree.PExpr;
using Nemerle.Compiler.Token;
using Nemerle.Imperative;

namespace Nemerle.Peg
{
  internal module IntelliSenseSupport
  {
    public AddIntelliSenseSupport(compiler : GrammarCompiler, parsedGrammar : Grammar, typer : Typer, tb : TypeBuilder) : void
    {
      foreach (name in parsedGrammar.Names)
        MakeGotoInfos(name, parsedGrammar, tb, typer);

      def gotoMap = IntelliSenseSupport.GetGotoMap(tb);
      foreach (kv in compiler.GetHandlerMethods())
      {
        gotoMap[kv.Key.Location]       = kv.Value.NameLocation;
        gotoMap[kv.Value.NameLocation] = kv.Key.Location;
      }
    }

    public GetGotoMap(tb : TypeBuilder) : Hashtable[Location, Location]
    {
      mutable gotoMap = tb.UserData["GoToInfoMap"] :> Hashtable[Location, Location];

      when (gotoMap == null)
      {
        gotoMap = Hashtable();
        tb.UserData["GoToInfoMap"] = gotoMap;
      }
      gotoMap
    }

    private MakeGotoInfos(ruleName : NameRef, grammar : Grammar, tb : TypeBuilder, _typer : Typer) : void
    {
      def gotoMap = GetGotoMap(tb);
      def makeGotoRuleDef(r : Rule) : void
      {
        | Choice(rules)                       => foreach (r in rules) makeGotoRuleDef(r)
        | Sequence(rules)                     => foreach (r in rules) makeGotoRuleDef(r)
        | Call(name, _)                       => gotoMap[name.Location] = grammar.GetRule(name).Location
        | RepeatMin(_, rule)                  => makeGotoRuleDef(rule)
        | RepeatMinMax(_, _, rule)            => makeGotoRuleDef(rule)
        | Chars | Fsm                         => ()
        | Not(rule)                           => makeGotoRuleDef(rule)
        | And(rule)                           => makeGotoRuleDef(rule)
        | Capture(_, rule)                    => makeGotoRuleDef(rule)
        | Cut                                 => ()
        | Scope(name, rule)                   =>
          def scopeBeginName = name.Id + GrammarCompiler.ScopeBeginSufix;
          def scopeEndName   = name.Id + GrammarCompiler.ScopeEndSufix;
          def isScopeHendler(m : ClassMember)
          {
            | Function as f => f.Name == scopeBeginName || f.Name == scopeEndName
            | _ => false
          }
          def scopeHendlers = tb.GetParsedMembers(false).Filter(isScopeHendler).Map(_ :> ClassMember.Function);
          match (scopeHendlers)
          {
            x :: _ => gotoMap[name.Location] = x.Location;
            | _ => Message.Error(name.Location, $"You must define scope hendler[s] for this scope: $scopeBeginName() : void and/or $scopeEndName(isFial : bool) : void");
          }
          makeGotoRuleDef(rule)
      }

      when (grammar.GetRule(ruleName).Rule is Some(rule))
        makeGotoRuleDef(rule);
    }
  }
}
