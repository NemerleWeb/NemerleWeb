using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace NemerleWeb.Peg
{
  partial internal class GrammarCompiler
  {
    public DefineAllMethods() : void
    {
      DefineGetMaxRollbackPos();
      DefineGetRuleName();
      DefineResetMaxRollbackPos();
      DefineResetMemoization();
      DefineParseMethods();

      _tb.Define(<[decl: mutable _parsingSource : NemerleWeb.Peg.SourceSnapshot; ]>);
      _tb.Define(<[decl:
        public ParsingSource : NemerleWeb.Peg.SourceSnapshot
        {
          get { _parsingSource }
        }
      ]>);
      
      PegGrammarOptions.GetDefineMethod(_tb)(<[decl:
        // should it be macro?
        protected GetText(tok : NemerleWeb.Peg.NToken) : string
        {
          _parsingSource.OriginalText.Substring(tok.StartPos, tok.EndPos - tok.StartPos)
        }
      ]>);
         
      PegGrammarOptions.GetDefineMethod(_tb)(<[decl:
        // should it be macro?
        protected GetLocation(tok : NemerleWeb.Peg.NToken) : NemerleWeb.Peg.Location
        {
          NemerleWeb.Peg.Location(_parsingSource, tok.StartPos, tok.EndPos)
        }
      ]>);
    }
  }
}
