using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Peg
{
  partial internal class GrammarCompiler
  {
    private DefineResetMemoization() : void
    {
      def clearMemoization = _definedMemoize.MapToList(keyValue =>
      {
        [ <[ $(keyValue.Value.MemPos    : dyn) = -1 ]>
        , <[ $(keyValue.Value.MemNewPos : dyn) = -1 ]>
        , <[ $(keyValue.Value.MemResult : dyn) = $(Typedtree.TExpr.DefaultValue() : typed) ]>
        ]
      }).Flatten();
       PegGrammarOptions.GetDefineMethod(_tb)(
        <[decl:
          private ResetMemoization() : void
          {
            ..$clearMemoization
          }
        ]>);
    }
  }
}
