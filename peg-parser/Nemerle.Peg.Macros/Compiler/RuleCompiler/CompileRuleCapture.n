using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Peg
{
  partial internal class RuleCompiler
  {
    public CompileRuleCapture(rule : Rule.Capture, results : list[string], global : bool, counter : int) : PExpr
    {
      match (rule)
      {
        | Capture(Argument(Chars), subrule) =>
          def code = CompileRule(subrule, [], global, counter);
          match (results)
          {
            | []       => code;
            | [result] =>
              def pos = if (global) <[ position ]> else <[ $(("pos" + counter) : usesite) ]>;
            
              // TODO: Restore
              def newPosCompileRuleCaptureChars = <[ $(("newPosCompileRuleCaptureChars" + counter) : usesite) ]>;
              <[
                def $newPosCompileRuleCaptureChars = $code;
                when ($newPosCompileRuleCaptureChars >= 0)
                  $(result : dyn) = Nemerle.Peg.NToken($pos, $newPosCompileRuleCaptureChars);
                $newPosCompileRuleCaptureChars
              ]>
            | _ => assert(false);
          }

        | Capture(Argument, subrule) => CompileRule(subrule, results, global, counter);
        | Capture(Handler(name, _, retType), subrule) =>
          match (results)
          {
            | []       => CompileRule(subrule, [], global, counter)
            | [result] =>
              def varNames     = MakeVariablesForRule(subrule);
              def subRulesCode = CompileRule(subrule, varNames, global, counter);

              def ty = match(retType : RuleType)
              {
                | NType(ty) => ty
                | _         => assert(false);
              }

              def omitLocation = _ruleToCompile.Attributes.Exists(_ is RuleAttribute.OmitLocation);
              def setResultLocation =
                if (!omitLocation && ty.TryRequire(_pegLocatedType))
                  match(varNames)
                  {
                    | []  with (start = <[ position ]>,
                                end   = <[ newPosCompileRuleCaptureHandler ]>)
                    | _   with (start = <[ Nemerle.Peg.Internal.EvalLocationStart($(varNames.Head : dyn), position) ]>,
                                end   = <[ Nemerle.Peg.Internal.EvalLocationEnd($(varNames.Last() : dyn), newPosCompileRuleCaptureHandler) ]>)
                      =>
                      <[ $(result : dyn).Location = Nemerle.Peg.Location(_parsingSource, $start, $end); ]>
                  }
                else
                  <[ () ]>;

              def names = _grammarCompiller.DefineMemoize(name, ty);

              def setResult =
                if (_grammarCompiller.GetHandlerMethods().Contains(name))
                  <[ $(result : dyn) = $(name.Id : dyn)(..$(varNames.Map(var => <[ $(var : dyn) ]>))) ]>
                else if (varNames.Count() == 1)
                  <[ $(result : dyn) = $(varNames.Head : dyn) ]>
                else
                  PExpr.Error("Too many variables.");
                  
              // TODO: Restore
              def newPosCompileRuleCaptureHandler = <[ $(("newPosCompileRuleCaptureHandler" + counter) : usesite) ]>;

              DefVars(varNames, <[
                {
                  if ($(names.MemPos : dyn) == position)
                  {
                    when ($(names.MemNewPos : dyn) >= 0)
                      $(result : dyn) = $(names.MemResult : dyn);
                    $(names.MemNewPos : dyn)
                  }
                  else
                  {
                    def $newPosCompileRuleCaptureHandler = $subRulesCode;

                    $(names.MemPos : dyn) = position;
                    $(names.MemNewPos : dyn) = $newPosCompileRuleCaptureHandler;

                    if ($newPosCompileRuleCaptureHandler >= 0)
                    {
                      $setResult;
                      $setResultLocation;
                      $(names.MemResult : dyn) = $(result : dyn);
                    }
                    else
                      when ($(names.MaxRollbackPos : dyn) < position)
                        $(names.MaxRollbackPos : dyn) = position;

                    $newPosCompileRuleCaptureHandler
                  }
                }
              ]>)
            | _ => assert(false);
          }
      }
    }
  }
}
