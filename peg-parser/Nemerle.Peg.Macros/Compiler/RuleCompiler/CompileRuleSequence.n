using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Peg
{
  partial internal class RuleCompiler
  {
    public CompileRuleSequence(rule : Rule.Sequence, results : list[string], global : bool, counter : int) : PExpr
    {
      def generate(rules : list[Rule], results : list[string], global : bool, counter : int) : PExpr
      {
        def pos = <[ $(("pos" + counter) : usesite) ]>;
        match (rules)
        {
          | [rule]        => CompileRule(rule, results, global, counter + 1);
          | rule :: rules =>
            match (rule)
            {
              | Capture(Handler(_, _, ty), _) when !(ty is RuleType.None || ty is RuleType.Void)
              | Capture(Argument(ty), _)      when !(ty is RuleType.None || ty is RuleType.Void)
                =>
                match (results)
                {
                  | result :: results =>
                    def ruleCode = CompileRule(rule, [result], global, counter + 1);
                    <[
                      {
                        def $pos = $ruleCode;
                        if ($pos >= 0) $(generate(rules, results, false, counter + 1)) else -1
                      }
                    ]>
                  | _ => PExpr.Error("Empty result list")
                }

              | _ =>
                def ruleCode = CompileRule(rule, [], global, counter + 1);
                <[
                  {
                    def $pos = $ruleCode;
                    if ($pos >= 0) $(generate(rules, results, false, counter + 1)) else -1
                  }
                ]>
            }

          | [] =>
            assert(results.Length == 0);
            <[ { $pos } ]>;
        }
      }

      def generateNoResult(rules : list[Rule], global : bool, counter : int) : PExpr
      {
        def pos = <[ $(("pos" + counter) : usesite) ]>;
        match (rules)
        {
          | []            => <[ { $pos } ]>
          | [rule]        => CompileRule(rule, [], global, counter + 1)
          | rule :: rules =>
            <[
              def $pos = $(CompileRule(rule, [], global, counter + 1));
              if ($pos >= 0) $(generateNoResult(rules, false, counter + 1)) else -1
            ]>
        }
      }

      match (results)
      {
        | [] => generateNoResult(rule.rules, global, counter);
        | _  => generate(rule.rules, results, global, counter);
      }
    }
  }
}
