using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Peg
{
  partial internal class RuleCompiler
  {
    private CompileRule(currentRule : Rule, results : list[string]) : PExpr
    {
      def code = match (currentRule)
      {
        | And          as rule => assert(results.IsEmpty()); CompileRuleAnd         (rule)
        | Not          as rule => assert(results.IsEmpty()); CompileRuleNot         (rule)
        | Fsm          as rule => assert(results.IsEmpty()); CompileRuleFsm         (rule)
        | Chars        as rule => assert(results.IsEmpty()); CompileRuleChars       (rule)
        | Call         as rule =>                            CompileRuleCall        (rule, results)
        | Sequence     as rule =>                            CompileRuleSequence    (rule, results)
        | Choice       as rule =>                            CompileRuleChoice      (rule, results)
        | RepeatMin    as rule =>                            CompileRuleRepeatMin   (rule, results)
        | RepeatMinMax as rule =>                            CompileRuleRepeatMinMax(rule, results)
        | Capture      as rule =>                            CompileRuleCapture     (rule, results)
        | Scope        as rule =>                            CompileRuleScope       (rule, results)
        | Cut          as rule =>                            CompileRuleCut         (rule, results)
      }

      if (_grammarCompiller.IsDebug)
        match (currentRule)
        {
          | Capture(Argument, _) => code
          | _                    => <[ _ = $($"$currentRule" : string); $code ]>
        }
      else
        code
    }

    public CompileRule() : PExpr
    {
      match (_ruleToCompile.Rule)
      {
        | Some(rule)=>
          if (_useResult)
          {
            match (_ruleToCompile.Attributes.Filter(_ is RuleAttribute.Recovery))
            {
              | [] =>
                <[
                  unchecked
                  {
                    mutable c : char;
                    _ = c; // eliminate warning "a local value c was never used"
                    $(CompileRule(rule, ["result"]))
                  }
                ]>

              | [recovery is RuleAttribute.Recovery] =>
                def skipRule = CompileRule(Rule.RepeatMin(0, Rule.Sequence([Rule.Not(recovery.stop), recovery.skip])), []);
                <[
                  unchecked
                  {
                    mutable c : char;
                    _ = c; // eliminate warning "a local value c was never used"
                    def newPos = $(CompileRule(rule, ["result"]));
                    if (newPos < 0)
                    {
                      def skipPos = $skipRule;
                      if (skipPos > pos)
                      {
                        $("result" : dyn) = $(recovery.handlerName.Id : usesite)(pos, skipPos, $(_ruleToCompile.Id : int));
                        ResetMaxRollbackPos();
                        skipPos
                      }
                      else
                        -1
                    }
                    else
                      newPos;
                  }
                ]>

              | _ =>
                Message.Error(_ruleToCompile.Location, "RuleAttribute.Recovery can be only one.");
                PExpr.Error();
            }
          }
          else
            <[
              unchecked
              {
                mutable c : char;
                _ = c; // eliminate warning "a local value c was never used"
                $(CompileRule(rule, []))
              }
            ]>
        | _ =>
          if (_useResult)
            <[
              unchecked
              {
                mutable c : char;
                _ = c; // eliminate warning "a local value c was never used"
                _ = result;
                -1
              }
            ]>
          else
            <[
              unchecked
              {
                mutable c : char;
                _ = c; // eliminate warning "a local value c was never used"
                -1
              }
            ]>
      }
    }
  }
}
